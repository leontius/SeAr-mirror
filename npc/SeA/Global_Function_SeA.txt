//1:zeny
//2:points
//3:cash
function	script	Give_to_All	{
	set .@para1,atoi(@cmd_para$[0]);
	if(!existitem(.@para1) && !(.@para1&3)) end;
	set .@para2,atoi(@cmd_para$[1]);
	if( set(.@num,query_sql2("select account_id from `char` where online = \"1\"",.@onlineaid)) > 0 )
		for( set .@i,0; .@i<.@num; set .@i,.@i+1 )
		{
			if(isloggedin(getd(".@onlineaid"+.@i)) == 1)
			{
				attachrid getd(".@onlineaid"+.@i);
				switch(.@para1)
				{
				case 1: set zeny, zeny+.@para2; break;
				case 2: atcommand "@points "+.@para2; break;
				case 3: atcommand "@cash "+.@para2; break;
				default: getitem .@para1,.@para2; break;
				}
			}
			if( !(.@i%100) ) sleep2 1; //prevent from infinite loops
		}
}

function	script	cmd_mapmove	{
	set .@m$,@cmd_para$[0];
	set .@x,atoi(@cmd_para$[1]);
	set .@y,atoi(@cmd_para$[2]);
	if( !existmap(.@m$) ) {
		dispmes msg(1);
		end;
	}
	if( !.@x && !.@y ) ; else
	if( checkcell(.@m$,.@x,.@y,cell_chknopass) ) {
		dispmes msg(2);
		set .@x,0;
		set .@y,0;
	}
	if( getmapflag(.@m$,mf_nowarpto) && getbattleflag("any_warp_GM_min_level") > getgmlevel() ) {
		dispmes msg(247);
		end;
	}
	if( getmapflag(.@m$,mf_nowarp) && getbattleflag("any_warp_GM_min_level") > getgmlevel() ) {
		dispmes msg(248);
		end;
	}
	if( warp(.@m$,.@x,.@y) ) {
		dispmes msg(1);
		end;
	}
	dispmes msg(0);	
}

function	script	cmd_jumpto	{
	set .@n$,@cmd_para$[0];
	set .@i,getcharid(3,.@n$);
	if( HP == 0 ) {
		dispmes "死亡时无法传送";
		end;
	}
	if( !isloggedin(.@i) ) {
		dispmes msg(3);
		end;
	}
	getmapxy .@m1$,.@x,.@y,0;
	if( getmapflag(.@m1$,mf_nowarp) && getbattleflag("any_warp_GM_min_level") > getgmlevel() ) {
		dispmes msg(248);
		end;
	}
	set .@j,getcharid(3);
	attachrid .@i;
	set .@n$, strcharinfo(0);
	getmapxy .@m2$,.@x,.@y,0;
	attachrid .@j;
	if( getmapflag(.@m2$,mf_nowarpto) && getbattleflag("any_warp_GM_min_level") > getgmlevel() ) {
		dispmes msg(247);
		end;
	}
	if( warp(.@m2$,.@x,.@y) ) {
		dispmes msg(1);
		end;
	}
	dispmes msg(4,.@n$);
}

function	script	cmd_jump	{
	set .@x,atoi(@cmd_para$[0]);
	set .@y,atoi(@cmd_para$[1]);
	if( HP == 0 ) {
		dispmes "死亡时无法传送";
		end;
	}
	getmapxy .@m$,.@i,.@j,0;
	if( !.@x && !.@y ) ; else
	if( checkcell(.@m$,.@x,.@y,cell_chknopass) ) {
		dispmes msg(2);
		set .@x,0;
		set .@y,0;
	}
	if( getmapflag(.@m$,mf_noteleport) && getbattleflag("any_warp_GM_min_level") > getgmlevel() ) {
		dispmes msg(248);
		end;
	}
	if( warp(.@m$,.@x,.@y) ) {
		dispmes msg(1);
		end;
	}
	getmapxy .@m$,.@x,.@y,0;
	dispmes msg(5,.@x,.@y);
}

function	script	cmd_where	{
	set .@n$,@cmd_para$[0];
	set .@i,getcharid(3,.@n$);
	if( !isloggedin(.@i) ) {
		dispmes msg(3);
		end;
	}
	if( getbattleflag("hide_GM_session") && getgmlevel() < getgmlevel(.@i) &&
		!(getbattleflag("who_display_aid") && getgmlevel() >= getbattleflag("who_display_aid"))
	) {
		dispmes msg(3);
		end;
	}
	set .@j,getcharid(3);
	attachrid .@i;
	set .@n$, strcharinfo(0);
	getmapxy .@m$,.@x,.@y,0;
	attachrid .@j;
	dispmes .@n$+" "+.@m$+" "+.@x+" "+.@y;
}

function	script	cmd_load	{
	if( getmapflag(getsavepoint(0), mf_nowarpto) &&  getbattleflag("any_warp_GM_min_level") > getgmlevel() ) {
		dispmes msg(249);
		end;
	}
	getmapxy .@m$,.@x,.@y,0;
	if ( getmapflag(.@m$, mf_nowarp) &&  getbattleflag("any_warp_GM_min_level") > getgmlevel() ) {
		dispmes msg(248);
		end;
	}
	warp(getsavepoint(0),getsavepoint(1),getsavepoint(2));
	dispmes msg(7);
}

function	script	cmd_delitem	{
	set .@i, atoi(@cmd_para$[0]);
	set .@j, atoi(@cmd_para$[1]);
	if(.@j < 1) set .@j, 1; 

	if( existitem(.@i) )
	{
		set .@k, countitem(.@i);
		if( .@k > .@j ) set .@k, .@j;
		dispmes msg(113,.@k);
		delitem .@i, .@k;
		end;
	}
	else if( existitem(@cmd_para$[0]) )
	{
		set .@k, countitem(@cmd_para$[0]);
		if( .@k > .@j ) set .@k, .@j;
		dispmes msg(113,.@k);
		delitem @cmd_para$[0], .@k;
		end;
	}
	dispmes msg(19);
	end;
}

function	script	cmd_monster	{
	if( existmob(atoi(@cmd_para$[0])) ) 
		set .@id, atoi(@cmd_para$[0]); 
	else if( existmob(@cmd_para$[0]) ) 
		set .@id, existmob(@cmd_para$[0]); 
	else
	{ 
		dispmes msg(40); 
		end; 
	} 

	if( .@id == 1288 )
	{ 
		dispmes msg(83); 
		end; 
	} 
	
	set .@id, .@id+getarg(0)*MAX_MOB_DB;

	set .@i,atoi(@cmd_para$[1]); 
	if(.@i < 1)
		set .@i, 1;
	if( set(.@j,getbattleflag("atc_spawn_quantity_limit")) && .@i > .@j ) 
		set .@i,.@j; 

	getmapxy .@m$,.@x,.@y,0; 
	areamonster .@m$, .@x-sqrt(.@i)-2, .@y-sqrt(.@i)-2, .@x+sqrt(.@i)+2, .@y+sqrt(.@i)+2, "--ja--", .@id, .@i; 
	dispmes msg(39);
}