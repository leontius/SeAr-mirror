//
//SeA孙燕姿
//非仿官方服务器请勿使用此脚本
//

-	script	SeA服务员::Son_SeA	-1,{
	cutin "son",2;
	mes "[SeA服务员]";
	mes "请你跟我说需要什么样的服务呢？";
	next;
	switch(select("状态服务","传送服务","职业服务",
		(
			(getgmlevel() < getbattleflag("gm_cant_drop_min_lv") && getbattleflag("gm_cant_drop_min_lv"))
			||
			(getgmlevel() > getbattleflag("gm_cant_drop_max_lv") && getbattleflag("gm_cant_drop_max_lv"))
		)
		?"悬赏系统":""))
	{
	case 4: doevent "Wanted_Reward::OnFunc";
		cutin "son",255;
		end;
	case 1:
		mes "[SeA服务员]";
		mes "我必将尽一切努力来为客人服务的。";
		next;
		mes "[SeA服务员]";
		percentheal 100,100;
		sc_end SC_Poison;
		sc_end SC_Silence;
		sc_end SC_Blind;
		sc_end SC_Confusion;
		sc_end SC_Curse;
		sc_end SC_Hallucination;
		mes "您的HP/SP已经恢复，负面状态已经解除，如果还有需要，随时欢迎回来找我喔!";
		break;
	case 2:
		mes "[SeA服务员]";
		mes "请告诉我你大致要去哪里";
		next;
		switch(select("城市传送","野外传送"))
		{
			case 1:
				mes "[SeA服务员]";
				mes "请问你想要去那里呢？";
				next;
/*
				set .@menu,select("普隆德拉","梦罗克","吉芬","斐扬","艾尔贝塔","依斯鲁得岛","艾尔帕兰","姜饼城","克魔岛","朱诺","樱花城",
					"昆仑","汶巴拉","尼芙菲姆","洛阳","","","爪哇咿","哎哟泰雅","艾音布罗克","里希塔乐镇","艾音贝赫","毁葛","拉赫",
					"伯仁斯","莫斯科")-1;
				atcommand "@go "+.@menu;
*/
				switch( select("普隆德拉","依斯鲁德","吉芬","梦罗克","斐扬","爱尔贝塔","艾尔帕兰","克魔岛","克雷斯特汉姆古城","朱诺",
					"天津","昆仑","汶巴拉","姜饼城","尼芙菲姆","洛阳城","夏威夷","哎哟泰雅","里希塔乐镇","艾音布罗克","艾音贝赫",
					"毁葛","拉赫","伯仁斯","莫斯科","马努克","拉皮内前线基地辉煌领域","米德加尔特联合驻地","巴西","埃迪斯特斯","Mora","德瓦他","Malangdo"
				) )
				{
				case 1: warp "prontera",156,187;	break;
				case 2: warp "izlude",128,111;	break;
				case 3: warp "geffen",119,66;	break;
				case 4: warp "morocc",159,93;	break;
				case 5: warp "payon",170,110;	break;
				case 6: warp "alberta",27,236;	break;
				case 7: warp "aldebaran",145,120;	break;
				case 8: warp "comodo",188,161;	break;
				case 9: warp "glast_01",200,200;	break;
				case 10: warp "yuno",160,168;	break;
				case 11: warp "amatsu",197,86;	break;
				case 12: warp "gonryun",150,130;	break;
				case 13: warp "umbala",130,130;	break;
				case 14: warp "xmas",148,131;	break;
				case 15: warp "niflheim",202,177;	break;	
				case 16: warp "louyang",210,108;	break;
				case 17: warp "jawaii",221,215;	break;	
				case 18: warp "ayothaya",149,118;	break;	
				case 19: warp "lighthalzen",158,92;	break;	
				case 20: warp "einbroch",64,200;	break;	
				case 21: warp "einbech",70,95;	break;	
				case 22: warp "hugel",96,145;	break;	
				case 23: warp "rachel",260,126;	break;	
				case 24: warp "veins",216,123;	break;
				case 25: warp "moscovia",223,184;	break;
				case 26: warp "manuk",266,202;	break;	
				case 27: warp "splendide",197,175;	break;
				case 28: warp "mid_camp",215,242;	break;
				case 29: warp "brasilis",226,99;	break;
				case 30: warp "dicastes01",197,181;	break;
				case 31: warp "mora",119,97;	break;
				case 32: warp "dewata",200,177;	break;
				case 33: warp "malangdo",217,85;	break;
				}
				break;
			case 2:
				mes "[SeA服务员]";
				mes "请问你想要去那里呢？";
				next;
				switch(select("迷藏森林","普隆德拉地下水道","海底洞窟","史芬克斯密穴","金字塔迷宫","蚂蚁地狱密穴","吉芬塔地下密穴",
					"妙勒尼废弃矿场","克雷斯特汉姆古城","兽人地下洞窟","裴扬迷宫","沉没之船","艾尔帕兰钟塔","龟岛","玩具工厂",
					"诺可罗德溶洞","樱花城迷宫","昆仑洞穴","古神木树洞","洛阳古都王陵","葛帔尼亚","优配擂斯废墟","机械娃娃工厂",
					"达纳托斯之塔","矿山洞穴","奥丁神殿","生体试验研究所","深渊湖水地下洞穴","冰洞穴","古代神殿迷宫","神殿圣域洞穴",
					"图尔火山洞窟","无名的海岛","地下公会","克魔岛密穴","深渊森林洞穴","次元空间","马努克旷野","辉煌领域旷野",
					"尼德霍格洞穴"))
				{
				case 1:
					set .@menu,select("迷藏森林1层","迷藏森林2层","迷藏森林3层")-1;
					setarray .@mapname$[0],"prt_maze01","prt_maze02","prt_maze03";
					break;
				case 2:
					set .@menu,select("普隆德拉地下水道1层","普隆德拉地下水道2层","普隆德拉地下水道3层","普隆德拉地下水道4层")-1;
					setarray .@mapname$[0],"prt_sewb1","prt_sewb2","prt_sewb3","prt_sewb4";
					break;
				case 3:
					set .@menu,select("海底洞窟1层","海底洞窟2层","海底洞窟3层","海底洞窟4层","海底洞窟5层")-1;
					setarray .@mapname$[0],"iz_dun00","iz_dun01","iz_dun02","iz_dun03","iz_dun04";
					break;
				case 4:
					set .@menu,select("史芬克斯密穴1层","史芬克斯密穴2层","史芬克斯密穴3层","史芬克斯密穴4层","史芬克斯密穴5层")-1;
					setarray .@mapname$[0],"in_sphinx1","in_sphinx2","in_sphinx3","in_sphinx4","in_sphinx5";
					break;
				case 5:
					set .@menu,select("金字塔迷宫地上1层","金字塔迷宫地上2层","金字塔迷宫地上3层","金字塔迷宫地上4层","金字塔迷宫地下1层","金字塔迷宫地下2层")-1;
					setarray .@mapname$[0],"moc_pryd01","moc_pryd02","moc_pryd03","moc_pryd04","moc_pryd05","moc_pryd06";
					break;
				case 6:
					set .@menu,select("蚂蚁地狱密穴1层","蚂蚁地狱密穴2层")-1;
					setarray .@mapname$[0],"anthell01","anthell02";
					break;
				case 7:
					set .@menu,select("吉芬塔地下密穴1层","吉芬塔地下密穴2层","吉芬塔地下密穴3层","吉芬塔地下密穴4层")-1;
					setarray .@mapname$[0],"gef_dun00","gef_dun01","gef_dun02","gef_dun03";
					break;
				case 8:
					set .@menu,select("妙勒尼废弃矿场1层","妙勒尼废弃矿场2层","妙勒尼废弃矿场3层")-1;
					setarray .@mapname$[0],"mjo_dun01","mjo_dun02","mjo_dun03";
					break;
				case 9:
					set .@menu,select("克雷斯特汉姆古城","修道院","地下公墓","地上1层","地上2层","监狱1层","监狱2层","骑士团1层","骑士团2层","室内","楼梯密穴","下水道1层","下水道2层","下水道3层","下水道4层","地下洞窟1层","地下洞窟2层")-1;
					setarray .@mapname$[0],"glast_01","gl_church","gl_chyard","gl_cas01","gl_cas02","gl_prison","gl_prison1","gl_knt01","gl_knt02","gl_in01","gl_step","gl_sew01","gl_sew02","gl_sew03","gl_sew04","gl_dun01","gl_dun02";
					break;
				case 10:
					set .@menu,select("兽人地下洞窟1层","兽人地下洞窟2层")-1;
					setarray .@mapname$[0],"orcsdun01","orcsdun02";
					break;
				case 11:
					set .@menu,select("裴扬迷宫1层","裴扬迷宫2层","裴扬迷宫3层","裴扬迷宫4层","裴扬迷宫5层")-1;
					setarray .@mapname$[0],"pay_dun00","pay_dun01","pay_dun02","pay_dun03","pay_dun04";
					break;
				case 12:
					set .@menu,select("沉没之船1层","沉没之船2层")-1;
					setarray .@mapname$[0],"treasure01","treasure02";
					break;
				case 13:
					set .@menu,select("艾尔帕兰钟塔地上1层","艾尔帕兰钟塔地上2层","艾尔帕兰钟塔地上3层","艾尔帕兰钟塔地上4层","艾尔帕兰钟塔地下1层","艾尔帕兰钟塔地下2层","艾尔帕兰钟塔地下3层","艾尔帕兰钟塔地下4层")-1;
					setarray .@mapname$[0],"c_tower1","c_tower2","c_tower3","c_tower4","alde_dun01","alde_dun02","alde_dun03","alde_dun04";
					break;
				case 14:
					set .@menu,select("龟岛表层","密穴1层","密穴2层","密穴3层")-1;
					setarray .@mapname$[0],"tur_dun01","tur_dun02","tur_dun03","tur_dun04";
					break;
				case 15:
					set .@menu,select("玩具工厂1层","玩具工厂2层")-1;
					setarray .@mapname$[0],"xmas_dun01","xmas_dun02";
					break;
				case 16:
					set .@menu,select("诺可罗德溶洞1层","诺可罗德溶洞2层")-1;
					setarray .@mapname$[0],"mag_dun01","mag_dun02";
					break;
				case 17:
					set .@menu,select("樱花城迷宫1层","樱花城迷宫2层","樱花城迷宫3层")-1;
					setarray .@mapname$[0],"ama_dun01","ama_dun02","ama_dun03";
					break;
				case 18:
					set .@menu,select("昆仑洞穴1层","昆仑洞穴2层","昆仑洞穴3层")-1;
					setarray .@mapname$[0],"gon_dun01","gon_dun02","gon_dun03";
					break;
				case 19:
					set .@menu,select("古神木树洞1层","古神木树洞2层")-1;
					setarray .@mapname$[0],"um_dun01","um_dun02";
					break;
				case 20:
					set .@menu,select("洛阳古都王陵1层","洛阳古都王陵2层","洛阳古都王陵3层")-1;
					setarray .@mapname$[0],"lou_dun01","lou_dun02","lou_dun03";
					break;
				case 21:
					set .@menu,select("葛帔尼亚1层","葛帔尼亚2层","葛帔尼亚3层","葛帔尼亚4层")-1;
					setarray .@mapname$[0],"gefenia01","gefenia02","gefenia03","gefenia04";
					break;
				case 22:
					set .@menu,select("优配擂斯废墟1层","优配擂斯废墟2层","优配擂斯中心部")-1;
					setarray .@mapname$[0],"juperos_01","juperos_02","jupe_core";
					break;
				case 23:
					set .@menu,select("机械娃娃工厂1层","机械娃娃工厂2层")-1;
					setarray .@mapname$[0],"kh_dun01","kh_dun02";
					break;
				case 24:
					set .@menu,select("达纳托斯之塔1层","达纳托斯之塔2层","达纳托斯之塔3层","达纳托斯之塔4层","达纳托斯之塔5层","达纳托斯之塔6层","达纳托斯之塔7层","达纳托斯之塔8层","达纳托斯之塔9层","达纳托斯之塔10层","达纳托斯之塔11层","达纳托斯之塔12层","达纳托斯之塔13层")-1;
					setarray .@mapname$[0],"tha_t01","tha_t02","tha_t03","tha_t04","tha_t05","tha_t06","tha_t07","tha_t08","tha_t09","tha_t10","tha_t11","tha_t12","thana_boss";
					break;
				case 25:
					set .@menu,select("矿山洞穴1层","矿山洞穴2层")-1;
					setarray .@mapname$[0],"ein_dun01","ein_dun02";
					break;
				case 26:
					set .@menu,select("奥丁神殿1层","奥丁神殿2层","奥丁神殿3层")-1;
					setarray .@mapname$[0],"odin_tem01","odin_tem02","odin_tem03";
					break;
				case 27:
					set .@menu,select("生体试验研究所1层","生体试验研究所2层","生体试验研究所3层")-1;
					setarray .@mapname$[0],"lhz_dun01","lhz_dun02","lhz_dun03";
					break;
				case 28:
					set .@menu,select("深渊湖水地下洞穴1层","深渊湖水地下洞穴2层","深渊湖水地下洞穴3层")-1;
					setarray .@mapname$[0],"abyss_01","abyss_02","abyss_03";
					break;
				case 29:
					set .@menu,select("冰洞穴1层","冰洞穴2层","冰洞穴3层")-1;
					setarray .@mapname$[0],"ice_dun01","ice_dun02","ice_dun03";
					break;
				case 30:
					set .@menu,select("古代神殿迷宫1层","古代神殿迷宫2层")-1;
					setarray .@mapname$[0],"ayo_dun01","ayo_dun02";
					break;
				case 31:
					set .@menu,select("神殿圣域洞穴1层","神殿圣域洞穴2层","神殿圣域洞穴3层","神殿圣域洞穴4层","神殿圣域洞穴5层")-1;
					setarray .@mapname$[0],"ra_san01","ra_san02","ra_san03","ra_san04","ra_san05";
					break;
				case 32:
					set .@menu,select("图尔火山洞窟1层","图尔火山洞窟2层","图尔火山洞窟3层")-1;
					setarray .@mapname$[0],"thor_v01","thor_v02","thor_v03";
					break;
				case 33:
					set .@menu,select("无名的海岛(昼)","无名的海岛(夜)","修道院1层","修道院2层","修道院3层")-1;
					setarray .@mapname$[0],"nameless_i","nameless_n","abbey01","abbey02","abbey03";
					break;
                                case 34:
					set .@menu,select("地下公会1层","地下公会2层","地下公会3层","地下公会4层")-1;
					setarray .@mapname$[0],"gld_dun01","gld_dun02","gld_dun03","gld_dun04";
					break;  
                                case 35:
					set .@menu,select("克魔岛密穴 西边","克魔岛密穴 北边","克魔岛密穴 东边")-1;
					setarray .@mapname$[0],"beach_dun","beach_dun2","beach_dun3";
					break;
                                case 36:
					set .@menu,select("深渊森林洞穴1层","深渊森林洞穴2层","深渊森林洞穴3层")-1;
					setarray .@mapname$[0],"mosk_dun01","mosk_dun02","mosk_dun03";
					break; 
                                case 37:
					set .@menu,select("次元空间1层","次元空间2层","次元空间3层")-1;
					setarray .@mapname$[0],"moc_fild20","moc_fild21","moc_fild22";
					break; 
                                case 38:
					set .@menu,select("马努克旷野1层","马努克旷野2层","马努克旷野3层")-1;
					setarray .@mapname$[0],"man_fild01","man_fild02","man_fild03";
					break; 
                                case 39:
					set .@menu,select("辉煌领域旷野1层","辉煌领域旷野2层","辉煌领域旷野3层")-1;
					setarray .@mapname$[0],"spl_fild01","spl_fild02","spl_fild03";
					break;
                                case 40:
					set .@menu,select("尼德霍格洞穴1层","尼德霍格洞穴2层")-1;
					setarray .@mapname$[0],"nyd_dun01","nyd_dun02";
					break; 
				}
				warp .@mapname$[.@menu],0,0;
				break;
		}
		break;
	case 3:
		mes "[SeA服务员]";
		mes "我可以帮你进行快速转职。";
		mes "我还负责相关的一些工作。";
		mes "请问你是来做什么的呢？";
		next;
		switch(select( "转职","转生","学习新技能","租用职业道具","点数重置","^FF0000领取三转职业道具^000000" ))
		{
			case 1:
				if( !BaseJob )
					set .@joblv_req,10;
				else if( (Class >= Job_Swordman && Class <= Job_Thief) ||
					 (Class >= Job_Swordman_High && Class <= Job_Thief_High) ||
					 (Class >= Job_Baby_Swordman && Class <= Job_Baby_Thief) )
					set .@joblv_req,40;
				else if( (Class >= Job_Knight && Class <= Job_Dancer) ||
					 (Class >= Job_Baby_Knight && Class <= Job_Baby_Dancer) )
					set .@joblv_req,50;
				else if( (Class >= Job_Lord_Knight && Class <= Job_Gypsy) )
					set .@joblv_req,50;
				else if( (BaseJob == Job_SuperNovice) )
					set .@joblv_req,99;
				else if( (Class == Job_Taekwon) )
					set .@joblv_req,40;

				if (SkillPoint || JobLevel < .@joblv_req) {
					mes "[SeA服务员]";
					mes "你的Job等级不够或者技能点数没有加完";
				} else {
					if(Class >= 4054) {
						mes "[SeA服务员]";
						mes "您的职业不能转职了";
						break;
					}
					if( BaseJob >= 7 && BaseJob <= 21 || BaseJob == Job_SuperNovice ) {
						if ( BaseLevel < 99 ) {
							mes "[SeA服务员]";
							mes "你的Base等级不够99级";
							break;
						}
						if( BaseJob <= 13 )
							jobchange BaseClass+4053, Upper;
						else if( BaseJob == Job_SuperNovice )
							jobchange 4190, Upper;
						else
							jobchange (BaseClass<=2?BaseClass:BaseClass+1)+4065, Upper;
					}
					else if ( Upper==1 )
						jobchange BaseJob?Ex_Cl:Ex_BC,Upper;
					else {
						mes "[SeA服务员]";
						mes "请问你是来转什么的呢？";
						next;
						if (!BaseJob)
							jobchange set(.@i,select("剑士","法师","弓箭手","服侍","商人","盗贼",BaseLevel>=45?"超级初心者":"",!Upper?"枪手":"",!Upper?"忍者":"",!Upper?"跆拳道":""))<7?.@i:(.@i>9?4046:(.@i+16)),Upper;
						else {
							switch (BaseJob)
							{
								case 1: jobchange set(.@i,select("骑士","十字军"))==1?(.@i+6):(.@i+12),Upper; break;
								case 2: jobchange set(.@i,select("巫师","贤者"))==1?(.@i+8):(.@i+14),Upper; break;
								case 3: jobchange set(.@i,select("猎人",Sex?"诗人":"舞娘"))==1?(.@i+10):(.@i+(Sex?17:18)),Upper; break;
								case 4: jobchange set(.@i,select("牧师","武僧"))==1?(.@i+7):(.@i+13),Upper; break;
								case 5: jobchange set(.@i,select("铁匠","炼金术师"))==1?(.@i+9):(.@i+16),Upper; break;
								case 6: jobchange set(.@i,select("刺客","流氓"))==1?(.@i+11):(.@i+15),Upper; break;
								case 4046: jobchange Class+select("拳圣","","灵媒"),Upper; break;
								default: mes "[SeA服务员]"; mes "当前职业不能再转职了!"; break;
							}
							if (.@i)
							{
								set Ex_BC,BaseClass;
								set Ex_Cl,Class;
							}
						}
					}
				}
				break;
			case 2:
				mes "[SeA服务员]";
				mes "基本等级不可以低于^FF000099级^000000.";
				mes "职业等级不可以低于^FF000050级^000000.";
				mes "负重不可超过^FF00001000^000000.";
				next;
				mes "[SeA服务员]";
				mes "转生后^FF0000你的所有级别将回到1级,变为进阶初心者^000000,转生以后可以得到^FF0000额外100点的属性点数^000000.";
				next;
				mes "[SeA服务员]";
				mes"请拭去之前的所有记忆,";
				mes"因为从现在起你所要面临的是一个";
				mes"新荣耀,和一个新的生命...";
				next;
				mes "[SeA服务员]";
				if ( Weight > 10000 ) {
					mes "你的负重为"+Weight/10+".";
					mes "请不要超过^FF00001000^000000负重.";
				} else if ( Upper ) {
					mes "你的身躯已经到了极限了!";
				} else if (class < 7 || class > 21 ) {
					mes "您的职业无法转生!";
				} else if ( Baselevel < 99 || JobLevel < 50) {
					mes "你尚未符合转生条件！";
				} else if (Zeny < 1258000) {
					mes "对不起，您没有足够的金钱，转生需要1258000z!";
				} else {
					nude;
					if(checkcart()) setcart 0;
					if(checkfalcon()) setfalcon 0;
					if(checkriding()) setriding 0;
					set Zeny,Zeny-1258000;
					jobchange 0,1;
					resetlvl 1;
					mes "恭喜你!!转生成功!!";
				}
				break;
			case 3:
				mes "[SeA服务员]";
				switch (BaseClass)
				{
					case 0:
						if (!BaseJob) {
							skill 142,1,0;
							skill 143,1,0;
						}
						break;
					case 1:
						skill 144,1,0;
						skill 145,1,0;
						skill 146,1,0;
						if (BaseJob==7) skill 1001,1,0;
						if (BaseJob==14) skill 1002,1,0;
						break;
					case 2:
						skill 157,1,0;
						if (BaseJob==9) skill 1006,1,0;
						if (BaseJob==16) {
							if(!getskilllv(1008) && !getskilllv(1017) && !getskilllv(1018) && !getskilllv(1019)) {
								mes "请选择一种元素更换技能";
								mes "(^FF0000一旦选择不可再做更改^000000)";
								next;
								switch(select("元素更换(水):元素更换(地):元素更换(火):元素更换(风)")) {
								case 1: skill 1008,1,0; break;
								case 2: skill 1017,1,0; break;
								case 3: skill 1018,1,0; break;
								case 4: skill 1019,1,0; break;
								}
								mes "[SeA服务员]";
							}
							skill 1007,1,0;
						}
						break;
					case 3:
						skill 147,1,0;
						skill 148,1,0;
						if (BaseJob==11) skill 1009,1,0;
						if (BaseJob==19) skill 1010,1,0;
						if (BaseJob==20) skill 1011,1,0;
						break;
					case 4:
						if (BaseJob==8) skill 1014,1,0;
						if (BaseJob==15) {
							skill 1015,1,0;
							skill 1016,1,0;
						}
						skill 156,1,0;
						break;
					case 5:	
						skill 153,1,0;
						skill 154,1,0;
						skill 155,1,0;
						if (BaseJob==10) {
							skill 1013,1,0;
							skill 1012,1,0;
						}
						if (BaseJob==18) skill 238,1,0;
						break;
					case 6:
						skill 149,1,0;
						skill 150,1,0;
						skill 151,1,0;
						skill 152,1,0;
						if (BaseJob==12) {
							skill 1003,1,0;
							skill 1004,1,0;
						}
						if (BaseJob==17) skill 1005,1,0;
						break;
				}
				mes "我已经把招数教给你了，打开技能表看一下吧";
				break;
			case 4:
				mes "[SeA服务员]";
				switch(BaseJob)
				{
					case Job_Blacksmith:
						if(Upper&4 && !checkriding()) { setarray .@option[0],2500,2,2255; break; }
					case Job_Alchemist:
					case Job_SuperNovice:
						setarray .@option[0],800,1,39; break;
					case Job_Knight:
						if(Upper&4) { setarray .@option[0],800,2,2007; break; }
					case Job_Crusader:
						setarray .@option[0],2500,2,63; break;
					case Job_Hunter:
						if(!(Upper & 4)) { 
							setarray .@option[0],2500,4,127; break;
						}
					default: mes "没有适合你的道具"; cutin "son",255; close;
				}
				if (Zeny<.@option[0]) {
					mes "对不起，你没有足够金钱。你必须有"+.@option[0]+"Zeny。";
				} else {
					if( ( .@option[3] && getskilllv(.@option[3]) < 1 ) || getskilllv(.@option[2]) < 1 )
					{
						mes "你必须学过相应的技能。";
						break;
					}
					set Zeny,Zeny-.@option[0];
					if (.@option[1]&1) setcart;
					if (.@option[1]&2) setriding;
					if (.@option[1]&4) setfalcon;
					mes "设置道具成功。";
				}	
				break;
			case 5:
				mes "[SeA服务员]";
				mes "我专门负责重置点数";
				mes "你想重置什么呢？";
				next;
				select("技能点(费用50w)","属性点(费用50w)","技能点和属性点(费用80w)");
				mes "[SeA服务员]";
				if(zeny >= set(.@i,(@menu==3?800000:500000))) {
					set Zeny,Zeny-.@i;
					if (@menu&1) {
						resetskill;
						sc_end SC_SPIRIT;
					}
					if (@menu&2) resetstatus;
					mes "已经重置好了";
				}
				else
					mes "你的钱不够";
				break;
			case 6:
				callsub S_Get3rdJobItems;
				break;
		}
		break;
	}
	cutin "son",255;
	close;

S_Get3rdJobItems:
	if(!(Upper&4)) {
		mes "[SeA服务员]";
		mes "您的职业无法领取三转道具。";
		return;
	}
	if(item_3rdjob) {
		mes "[SeA服务员]";
		mes "您已经领取过三转道具了。";
		return;
	}
	getitem 2795,1; // 青苹果戒指
	switch(BaseJob)
	{
	case Job_Knight:
		getitem 5746,1; // 符文 头环
		break;
	
	case Job_Hunter:
		getitem 5748,1; // 狙击手 护目镜
		getitem 6124,1; // 狼笛
		//getitem 5516,1; // 鹰眼
		break;

	case Job_Priest:
		getitem 5747,1; // 圣冠
		break;

	case Job_Wizard:
		getitem 5753,1; // 魔力之 石 帽子
		break;

	case Job_Blacksmith:
		if(Sex)
    		getitem 5749,1; // 驾驶员 头带
    	else
    		getitem 5760,1; // 驾驶员 头带
		break;

	case Job_Assassin:
		getitem 5755,1; // 沉默 执行者
		break;

	case Job_Crusader:
		getitem 5757,1; // 覆面 施密特 头盔
		break;

	case Job_Bard:
		getitem 5751,1; // 宫廷乐师 宋的 帽子
		break;

	case Job_Dancer:
		getitem 5758,1; // 濒死的天鹅
		break;

	case Job_Monk:
		getitem 5754,1; // 燃烧的 灵魂
		break;

	case Job_Sage:
		getitem 5756,1; // 风之 低语
		break;

	case Job_Alchemist:
		getitem 5752,1; // 麦得斯的 低语
		break;

	case Job_Rogue:
		getitem 6121,1; // 化妆刷
		getitem 6122,1; // 毛刷
		getitem 5750,1; // 影子 工艺
		break;
	}
	set item_3rdjob,1;
	return;

OnTouch:
	percentheal 100,100;
	sc_end SC_Poison;
	sc_end SC_Silence;
	sc_end SC_Blind;
	sc_end SC_Confusion;
	sc_end SC_Curse;
	sc_end SC_Hallucination;
	emotion 2;
	end;
}

//"普隆德拉"
prontera,146,92,5	duplicate(Son_SeA)	SeA服务员#1	756,15,15
//"依斯鲁德岛"
izlude,134,92,3	duplicate(Son_SeA)	SeA服务员#2	756,15,15
//"艾尔帕兰"
aldebaran,143,116,3	duplicate(Son_SeA)	SeA服务员#3	756,15,15
//"吉芬"
geffen,123,62,8	duplicate(Son_SeA)	SeA服务员#4	756,15,15
//"梦罗克"
morocc,160,97,3	duplicate(Son_SeA)	SeA服务员#5	756,15,15
//"爱尔贝塔"
alberta,25,238,3	duplicate(Son_SeA)	SeA服务员#6	756,15,15
//"斐扬"
payon,184,104,3	duplicate(Son_SeA)	SeA服务员#7	756,15,15
//"尤诺"
yuno,164,48,3	duplicate(Son_SeA)	SeA服务员#8	756,15,15
//"天津"
amatsu,192,81,5	duplicate(Son_SeA)	SeA服务员#9	756,15,15
//"昆仑"
gonryun,156,14,5	duplicate(Son_SeA)	SeA服务员#10	756,15,15
//"姜饼城"
xmas,149,135,5	duplicate(Son_SeA)	SeA服务员#11	756,15,15
//"洛阳"
louyang,224,60,5	duplicate(Son_SeA)	SeA服务员#12	756,15,15
//"尼芙菲姆"
niflheim,21,159,5	duplicate(Son_SeA)	SeA服务员#13	756,15,15
//"夏威夷"
jawaii,223,204,4	duplicate(Son_SeA)	SeA服务员#14	756,15,15
//"莫斯科"
moscovia,226,191,3	duplicate(Son_SeA)	SeA服务员#15	756,15,15
//"伯仁斯"
veins,215,123,5	duplicate(Son_SeA)	SeA服务员#16	756,15,15
//"艾音贝赫"
einbech,77,103,3	duplicate(Son_SeA)	SeA服务员#17	756,15,15
//"拉赫"
rachel,134,99,3	duplicate(Son_SeA)	SeA服务员#18	756,15,15
//"修吉尔"
hugel,99,168,3	duplicate(Son_SeA)	SeA服务员#19	756,15,15
//"艾音布罗克"
einbroch,69,203,3	duplicate(Son_SeA)	SeA服务员#20	756,15,15
//"里希塔勒镇"
lighthalzen,153,100,5	duplicate(Son_SeA)	SeA服务员#21	756,15,15
//"克魔岛"
comodo,194,156,3	duplicate(Son_SeA)	SeA服务员#22	756,15,15
//"汶巴拉"
umbala,90,159,5	duplicate(Son_SeA)	SeA服务员#23	756,15,15
//"泰国"
ayothaya,145,117,5	duplicate(Son_SeA)	SeA服务员#24	756,15,15
//
