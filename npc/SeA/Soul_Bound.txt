//===== SeAthena Script =======================================
//= Soul Bound
//===== By: ==================================================
//= 5511, Inkfish, martin6688
//===== 当前版本: ============================================
//= 1.1
//===== 适合: ================================================
//= SeAthena 1.1.24
//===== 描述: ================================================
//= 1.0 初步完成脚本		by 5511
//= 1.1 cRO官方对话		by martin6688
//	NPC形象位置、整理整合	by Inkfish
//===== 备注: ================================================
//= 
//============================================================

prontera,165,60,4	script	实习附魔师	73,{
	mes "[实习附魔师]";
	mes "如果带着2件以上的铠甲过来结果出了";
	mes "意外，我将不负责任";
	next;

	switch( select("无洞防具:有洞防具:高级系列防具:下次再说") )
	{
	case 1: 
		set .@H_Zeny,100000;
		setarray .@H_Item_DB[0],2307,2309,2314,2316,2321,2325,2327,2330,2332,2334,2335,2341,2344,2346,2348,2350,2394,2395,2396,2337,2386;
		break;
	case 2: 
		set .@H_Zeny,1000000;
		setarray .@H_Item_DB[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351; 
		break;
	case 3: 
		set .@H_Zeny,10000000;
		setarray .@H_Item_DB[0],2364,2365,2374,2375,2387,2388,2389,2391,2390,2376,2377,2378,2379,2380,2381,2382; 
		break;
	case 4: close;
	}

	mes "[实习附魔师]";
	mes "那么你想要给哪件物品施加魔法.";
	next;

	set .@H_Menu$, "";
	set .@H_Count, getarraysize(.@H_Item_DB);
	for( set .@i, 0; .@i < .@H_Count; set .@i, .@i + 1 ) {
		if ( getitemslots(.@H_Item_DB[.@i]) )
			set .@H_Menu$, .@H_Menu$ + getitemname(.@H_Item_DB[.@i]) + "["+getitemslots(.@H_Item_DB[.@i])+"]:";
		else
			set .@H_Menu$, .@H_Menu$ + getitemname(.@H_Item_DB[.@i]) + ":";
	}

	set .@H_Item_ID, .@H_Item_DB[select(.@H_Menu$) - 1];

	mes "[实习附魔师]";
	mes "你额定要为 ^3355FF" +getitemname(.@H_Item_ID)+ " ^000000附魔吗？";
	mes "费用是 ^FF0000" +.@H_Zeny+ "^000000 Zeny";
	//mes "或者^3355FF 1 ^000000个^FF0000 " +getitemname(6242)+ " ^000000";
	next;
	if (select("再考虑一下:已经做好准备，请帮我附魔") == 1 ) {
		mes "[实习附魔师]";
		mes "那么下次有机会再见";
		close;
	}
	
	mes "[实习附魔师]";
	mes "你想要给这件^0000FF" + getitemname(.@H_Item_ID) + "^000000施加魔法吗?";
	next;
	mes "[实习附魔师]";
	mes "最重要的是，";
	mes "^FF0000当前精炼值和原来的装备";
	mes "卡片会全部消失^000000.";
	mes "那么你确定要进行附魔吗?";
	next;

	if (select("再考虑一下:已经做好准备，请帮我附魔") == 1 ) {
		mes "[实习附魔师]";
		mes "那么下次有机会再见";
		close;
	}

	if ( countitem(.@H_Item_ID) < 1 ) {
		mes "[实习附魔师]";
		mes "抱歉, 要知道我不可能把魔法加";
		mes "在你的身上, 请带来你的衣服.";
		close;		
	}
	if ( Zeny < .@H_Zeny ) {
		if ( !countitem(6242) ) {
			mes "[实习附魔师]";
			mes "抱歉, 你没有足够的金钱.";
			close;		
		}
		delitem 6242,1;
		set .@flag,1;
	}
	if( !.@flag )
		set zeny,zeny - .@h_zeny;
	delitem .@h_item_id,1;

	mes "[实习附魔师]";
	mes "那我们开始吧";
	close2;

	pcblockmove getcharid(3),1;
	progressbar "ffff00",10;
	pcblockmove getcharid(3),0;

	set .@H_r,rand(1,100);
	if(.@H_r >= 90) set .@H_Enchant,2;
	else if(.@H_r >= 70) set .@H_Enchant,1;
	else if(.@H_r >= 30) set .@H_Enchant,0;
	else {
		misceffect 306; 
		mes "[实习附魔师]";
		mes "喔, 真是太不幸了!";
		mes "我说过这是会失败的...";
		close;
	}
	misceffect 305; 
	mes "[实习附魔师]";
	mes "成功了!! 非常漂亮!";
	getitem2 .@H_Item_ID,1,1,0,0,0,0,0,callfunc("F_RandMes",6,4700,4710,4720,4730,4740,4750)+.@H_Enchant;
	close;
}