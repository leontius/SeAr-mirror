//===== eAthena Script ======================================= 
//= Black Jack
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//=
//===== Description: ========================================= 
//= Black Jack card game.  Gameplay based off standard casino
//= black jack rules.  Dealer must have at least 17 to stay and will
//= automatically stay at 17 and up.  Player must have at least
//= 13 to stay.  Aces counted as 11 or 1.  Option to "Double Down".
//= Currently does not allow for "insurance", or "splitting"
//= of pairs.
//===== Additional Comments: ================================= 
//=
//汉化:wack
//感谢jack9685以及激情RO3F玩家提供的资料
//============================================================ 


cmd_in02.gat,188,89,1	script	21点庄家	57,{

	mes "[21点庄家]";
	mes "你好! 想不想来玩一把21点?";
	M_Menu:
	next;
	menu "游戏规则是怎么样的?",M_0, "我要玩.",L_Play, "不，谢了.",M_End;

	M_0:
		mes "[21点庄家]";
		mes "你想要了解哪一方面的?";
		sM_Menu0:
		next;
		menu "基础知识。",sM_0a, "胜负规则",sM_0b, "'加倍'。",sM_0c,
			"'Ａ'的值。",sM_0d, "没什么。",M_Menu;

		sM_0a:
			mes "[21点庄家]";
			mes "21点就是一个纸牌游戏，这游戏的目标就是取得21点或者是更接近21点，当然不能超过21点。";
			next;
			mes "[21点庄家]";
			mes "开始游戏前，玩家必须下注。一旦下注，庄家和玩家每人将发到2张牌。";
			mes "根据你手上的牌你可以选择 ^5533FF'停牌'^000000,或者^5533FF'要牌'^000000。";
			next;
			mes "[21点庄家]";
			mes "当你选择'停牌',就是告诉庄家你需要再加任何牌了。";
			mes "然后庄家选择是否要牌。 ";
			mes "你必须至少达到^FF3355 13点 ^000000才可以停牌。";
			mes "庄家必须要到^FF3355 17点或者17点以上 ^000000才可以停牌。";
			next;
			mes "[21点庄家]";
			mes "当你选择'要牌',就是告诉庄家你还要加一张牌。";
			mes "你可以通过要牌来增加你的点数.";
			mes "只要你没超过^FF3355 21 ^000000点，你就一直可以要牌。";
			goto sM_Menu0;
		sM_0b:
			mes "[21点庄家]";
			mes "这里有几条胜负规则：";
			next;
			mes "[21点庄家]";
			mes "1.) 在21点最后1回合中：如果你的点数大于庄家的，那么你就赢了。反之，如果你的点数小于庄家的，那么你就输了。";
			next;
			mes "[21点庄家]";
			mes "2.) 如果你要了1张牌而使你的点数超过了21点，那么你就输掉这局。这个就叫作^5533FF'爆牌'^000000。";
			mes "如果庄家爆牌，那么你就赢得了这局。";
			next;
			mes "[21点庄家]";
			mes "3.) 如果你拿的头2张牌就21点了，那么你就赢得了这局。";
			mes "当你头2张拿到1张'Ａ'和1张值为10点的牌,这个就叫作^5533FF'Black Jack'^000000牌型。";
			mes "当庄家拿到'Black Jack',那么就是庄家赢了。";
			next;
			mes "[21点庄家]";
			mes "4.) 除了胜负，还有就是平局。";
			mes "当你和庄家都是相同的点数时，这种就是没有胜负没有输赢的平局";
			next;
			mes "[21点庄家]";
			mes "这个就叫你和庄家打成^5533FF'平手'^000000。";
			mes "同样，你和庄家同时拿到'Black Jack'也是这种情况。";
			goto sM_Menu0;
		sM_0c:
			mes "[21点庄家]";
			mes "'加倍'就是把你的下注加倍,但是这样你就只能再要1张牌。";
			mes "这个只能在每局的第一回合中使用。";
			next;
			mes "[21点庄家]";
			mes "举1个加倍很有用的例子就是当你一开始的2张牌加起来是11点时。";
			mes "拿1下张牌，你很有可能得到21点或者20点。";
			mes "这个就是加倍的好时机。";
			goto sM_Menu0;
		sM_0d:
			mes "[21点庄家]";
			mes "'Ａ'是在21点中唯一一张特殊的牌，因为它有2个值。";
			mes "'Ａ'可以当作11点也可以当作1点。";
			mes "举例来说，你拿了1张4和1张A,那么你得到的点数可以说是15点，也可以说是5点。";
			next;
			mes "[21点庄家]";
			mes "如果你选择停牌，那么A将自动被认为是11点，你得到总点数为15点";
			next;
			mes "[21点庄家]";
			mes "如果你选择要牌然后得到了1张9，那么A将自动作为1点，你的总点数为14点。";
			mes "如果A作为11点来算，你的点数就超过21点了，将马上输掉这个比赛";
			next;
			mes "[21点庄家]";
			mes "因为A在游戏里可以有2个不同的值，";
			mes "所以这是1张非常厉害的牌";
			goto sM_Menu0;
	M_End:
		mes "[21点庄家]";
		mes "无论何时都欢迎您的到来";
		close;

//================
L_Play:
	mes "[21点庄家]";
	mes "请你下注...";
	next;
	menu "2z",M_1a, "10z",M_1b, "20z",M_1c, "100z",M_1d, "我输的够惨了....",M_End;

	M_1a:
		if(Zeny < 2) goto sL_NotEnuf;
		set @bet, 2;
		goto L_Cont0;
	M_1b:
		if(Zeny < 10) goto sL_NotEnuf;
		set @bet, 10;
		goto L_Cont0;
	M_1c:
		if(Zeny < 20) goto sL_NotEnuf;
		set @bet, 20;
		goto L_Cont0;
	M_1d:
		if(Zeny < 100) goto sL_NotEnuf;
		set @bet, 100;
		goto L_Cont0;

		sL_NotEnuf:
			mes "[21点庄家]";
			mes "对不起，你没有足够的钱来下住";
			close;

	L_Cont0:
	mes "(已有的牌)";
	next;
	deletearray $@card[0],13;
	set @dealerTurn, 0;
	set @numP, 0;
	set @numD, 0;
	set @pAce, 0;
	set @dAce, 0;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;

//==============
L_Start:
	callsub sF_GetTot;

	mes "- 这个是 ^FF5533庄家^000000 的牌:";
	if (@numD==2) callsub sF_D2cards;
	if (@numD==3) callsub sF_D3cards;
	if (@numD==4) callsub sF_D4cards;
	if (@numD==5) callsub sF_D5cards;
	mes "   庄家有: ^FF5533"+@dealTot+"^000000";
	mes " ";
	mes "- 这个是 ^5533FF你^000000 的牌:";
	if (@numP==2) callsub sF_P2cards;
	if (@numP==3) callsub sF_P3cards;
	if (@numP==4) callsub sF_P4cards;
	if (@numP==5) callsub sF_P5cards;
	if(@pAce != 1 || @playTot == 21) mes "   你有: ^5533FF" +@playTot+ "^000000";
	if(@pAce == 1 && @playTot != 21) mes "   你有: ^5533FF" +@playTot+ "^000000, 或者 ^5533FF" +(@playTot-10)+ "^000000";
	next;
	if(@playTot==21 && @dealTot==21) goto sL_Push;
	if(@numP==2 && @playTot == 21) goto sL_Win;
	if(@numD==2 && @dealTot == 21) goto sL_Lose;
	if(@playTot > 21) goto sL_Lose;
	if(@dealTot > 21) goto sL_Win;
	if(@numP==2 && @dealerTurn==0) menu "点击(要牌)",M_Hit, "停牌",M_Stay, "加倍",M_Double;
	if(@dealerTurn == 0) menu "点击(要牌)",M_Hit, "停牌",M_Stay;

	M_Stay:
		mes "[21点庄家]";
		if(@playTot < 13) goto sL_PlayToLow;
		if(@dealTot > 16) mes "庄家停牌了.";
		if(@dealTot > 16 || @numD == 5) goto L_Check;
		mes "庄家要牌...";
		next;
		callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
		set @dealerTurn, 1;
		goto L_Start;

		sL_PlayToLow:
			mes "你的点数太低不能停牌，你必须要牌";
			next;
			goto M_Hit;

	M_Hit:
		if(@numP == 5) goto M_Stay;
		callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
		goto L_Start;
	M_Double:
		mes "[21点庄家]";
		mes "玩家选择了加倍. 你现在的赌注将加倍，";
		mes "只能再给你加1张牌";
		next;
		set @dealerTurn, 1;
		set @bet, @bet*2;
		callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
		goto L_Start;

//=============
L_Check:
	next;
	if(@playTot < @dealTot) goto sL_Lose;
	if(@playTot == @dealTot) goto sL_Push;

	sL_Win:
		mes "[21点庄家]";
		mes "祝贺你, 你赢了!";
		next;
		set Zeny, Zeny + @bet;
		goto L_Play;
	sL_Lose:
		mes "[21点庄家]";
		mes "你输了";
		set Zeny, Zeny - @bet;
		next;
		goto L_Play;
	sL_Push:
		mes "[21点庄家]";
		mes "这是平局，你和庄家打平。";
		next;
		goto L_Play;


//==================================
// Sub function for dealing/pulling the cards
sF_GetCards:
	set @rnd, rand(1,13);
	if($@card[@rnd] == 4) goto sF_GetCards;
	set $@card[@rnd], $@card[@rnd] + 1;
	set getarg(1), @rnd;
	if(getarg(1) > 10) set getarg(1), 10;
	if(getarg(1) == 1 && getarg(3) < 1) set getarg(1), 11;
	if(getarg(1) == 11) set getarg(3), 1;
	set getarg(2), " " + getarg(1) + " ";
	if(@rnd == 10) set getarg(2), getarg(1);
	if(@rnd == 1) set getarg(2), " A ";
	if(@rnd == 11) set getarg(2), " J ";
	if(@rnd == 12) set getarg(2), " Q ";
	if(@rnd == 13) set getarg(2), " K ";
	set getarg(0), getarg(0) + 1;
	return;

//==================================
// Sub function for getting the total score for each hand
sF_GetTot:
	set @i, 0;
	set @dealTot, 0;
	set @playTot, 0;

	GetDealTot:
		set @dealTot, @dealTot + @dealCard[@i];
		set @i, @i + 1;
		if(@i < @numD) goto GetDealTot;
		if(@dAce == 1 && @dealTot > 21) set @dealTot, @dealTot - 10;
		set @i, 0;
	GetPlayTot:
		set @playTot, @playTot + @playCard[@i];
		set @i, @i + 1;
		if(@i < @numP) goto GetPlayTot;
		if(@pAce == 1 && @playTot > 21) set @pAce, 2;
		if(@pAce > 0 && @playTot > 21) set @playTot, @playTot - 10;
	return;

//=======================================
// Sub function for displaying the Cards
sF_D2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_D3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" |   | "+@dealCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_P3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" |   | "+@playCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_D4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@dealCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_P4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" |";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@playCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_D5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[3]+" |   | "+@dealCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[3]+" |   | "+@playCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
}
