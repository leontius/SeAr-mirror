//===== SeAthena Script ======================================= 
//= 拆卡 NPC
//===== By: ================================================== 
//= TyrNemesis^
//===== Current Version: ===================================== 
//= 1.2.1
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= Removes cards from equipped items.
//===== Additional Comments: ================================= 
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2.1 汉化. [Inkfish@SeA]
//============================================================ 

prt_in,28,73,4	script	智慧的老妇人#SeA	78,{
	mes "[智慧的老妇人]";
	mes "你好，年轻人。我能帮你把插入装备的卡片拆下来。有兴趣吗？";
	next;
	switch(select("有兴趣哇。:要报酬吗?:不了，谢谢。")) {
		case 1:
			mes "[智慧的老妇人]";
			mes "好的。那你要在哪件物品上做试验?";
			next;

			setarray .@position$[1], "头上","身体","左手","右手","披肩","鞋子","装饰品1","装饰品2","头中","头下";
			set .@menu$,"";
			for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
			{
				if( getequipisequiped(.@i) )
					set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

				set .@menu$, .@menu$ + ":";
			}
			set .@part,select(.@menu$);
			if(!getequipisequiped(.@part)) {
				mes "[智慧的老妇人]";
				mes "年轻人... 你哪里什么都不穿叫我怎么拆卡？";
				close;
			}
			if(getequipcardcnt(.@part) == 0) {
				mes "[智慧的老妇人]";
				mes "年轻人... 你那件装备没有插卡。那我想我什么都不能做咯！";
				close;
			}
			if(isrental(.@part)) {
				mes "[智慧的老妇人]";
				mes "年轻人... 你那件装备好像不是你自己的！拆坏了怎么办呀？";
				close;
			}
			//不检测绑定的卡片
			if( isrestricted(getequipid(.@part)) || islocked(getequipid(.@part)) ) {
				mes "[智慧的老妇人]";
				mes "年轻人... 你那件装备好像不能弄丢啊！拆坏了会不会有人来找我麻烦？";
				close;
			}
			set .@cardcount,getequipcardcnt(.@part);
			
			if (!checkweight(1202,(.@cardcount+1))) { 
				mes "^3355FF等一下!";
				mes "我无法帮助你，因为你携带了太多的东西~";
				close;
			}
			mes "[智慧的老妇人]";
			mes "这件道具有 " + .@cardcount + " 张卡片。为了施法，我需要 " + (200000+(.@cardcount * 25000)) + " zeny, 一个 ^0000FF星星的角^000000, 和一个 ^0000FF黄色魔力矿石^000000.";
			next;
			if(select("好的，来吧:算了") == 2) {
				mes "[智慧的老妇人]";
				mes "好吧。等你想来的时候再来找我";
				close;
			}
			mes "[智慧的老妇人]";
			mes "施法前我必须警告你：我会失败。 如果我失败了，可能装备，或者卡片，或者他们全部都会消失。我可不会赔你哦，如此一来，你要先告诉我，卡片和装备谁重要？";
			next;
			switch(select("我改主意了:装备:卡片")) {
				case 1:
					mes "[智慧的老妇人]";
					mes "好吧。等你想来的时候再来找我";
					close;
				case 2:
					set .@failtype,1;
					break;
				case 3:
					set .@failtype,2;
			}
			mes "[智慧的老妇人]";
			mes "好的，我要开始了";
			next;
			mes "[智慧的老妇人]";
			if( getequipcardcnt(.@part) != .@cardcount ) {
				mes "你给我的东西好像不是刚才了的嘛！";
				debugmes strcharinfo(0)+" 在拆卡时试图在中途更换装备";
				close;
			}
			if((zeny < (200000+(.@cardcount * 25000))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
				mes "你没有我施法需要的东西哦，孩子。等你有了再来找我吧";
				close;
			}
			set zeny,zeny - (200000+(.@cardcount * 25000));
			delitem 1000,1;
			delitem 715,1;
			
			// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
			// First value = Total failure chance (item and cards destroyed)
			// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
			// Third value = Harmless failure chance (all that's lost is your investment)

			set .@failchance,rand(100);
	
			if(.@failchance < 2) {
				failedremovecards .@part,0;
				mes "完全失败了！可能所有东西都坏了。";
			}
			else
			if(.@failchance < 8) {
				if (.@failtype == 1) {
					failedremovecards .@part,1;	
					mes "我试图将卡片拆除的时候，卡片坏了，但是装备还是好的。";
				}
				else
				if (.@failtype == 2) {
					failedremovecards .@part,2;
					mes "很不幸，拆卡是成功了，但是装备本身坏了。";
				}
			}
			else
			if(.@failchance < 10) {
				failedremovecards .@part,3;
				mes "拆卡失败了。很幸运的是，他们都没有损坏。";
			}
			else
			{
				successremovecards .@part;
				mes "拆卡成功，这是你的卡片和装备。再见。";
			}
			close;			
		case 2:
			mes "[智慧的老妇人]";
			mes "起价 200000 zeny, 每张卡片另收 25000 zeny 。另外，我需要星星的角和黄色魔力矿石来辅助施法。";
			close;
		case 3:
			mes "[智慧的老妇人]";
			mes "好吧。等你想来的时候再来找我";
			close;
	}
}