//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 2008 鼠来献宝庆新年 -
//===================================
// 版本：1.0
//===================================
// [笨冰]:照官方修正NPC位置 
// 官方实际物品消耗量 米袋2个 --兑换-> 糯米1个
// 但是身上只有一个米袋时，依旧不会触发兑换失败的部分
// 只有不能兑换成功而已
// 甜米糕(554)贩卖关闭，官方使用的是商城系统(1P-1个)
// 修正NPC面向为0，与台湾官方伺服器相同
//====================================

payon_in01,193,30,0	script	糕饼 奶奶#rat	78,{
	mes "[糕饼 奶奶]";
	if (!lunar_rat) {
		mes "唉哟.. 唉哟...";
		mes "这要怎么办才好呢...";
		next;
		mes "[糕饼 奶奶]";
		mes "咳嗽! 咳嗽!";
		next;
		mes "[糕饼 奶奶]";
		mes "你们这些坏家伙们...";
		next;
		select("奶奶请问有什么心事吗?");
		mes "[糕饼 奶奶]";
		mes "年轻人听一听我说的话吧，";
		mes "为了这一次的过年所准备的糕饼材料";
		mes "全部被这些坏老鼠们给偷走了..";
		next;
		mes "[糕饼 奶奶]";
		mes "咳嗽! 咳嗽!";
		next;
		mes "[糕饼 奶奶]";
		mes "没有材料的话是做不出糕饼的";
		mes "这要怎么办才好呢..";
		next;
		select("请问材料是什么啊?");
		mes "[糕饼 奶奶]";
		mes "那个就是..";
		mes "装满了米的米袋啊..";
		mes "我儿子要用米 辗米才行的，";
		mes "若没有米的话 不能做出任何东西啊..";
		mes "不知年轻人可否能帮我啊?";
		next;
		if (select("当然啊:我不想啊") == 1) {
			set lunar_rat,1;
			mes "[" + strcharinfo(0)+"]";
			mes "奶奶请问我要怎么帮你才可以呢?";
			next;
			mes "[糕饼 奶奶]";
			mes "谢谢您 年轻人，";
			mes "偷走我米袋的家伙们";
			mes "应该是白鼠和蓝鼠";
			mes "还有土拨鼠准没错的，";
			mes "抓到那些家伙把他们偷走的米袋";
			mes "拿来的话就可以啦";
			next;
			select("请不要担心");
			mes "[" + strcharinfo(0)+"]";
			mes "奶奶 请不要担心，";
			mes "若找到的话 我就马上把它带回来的";
			next;
			mes "[糕饼 奶奶]";
			mes "麻烦你啊....";
			mes "就麻烦你啦 年轻人";
			close;
		}
		mes "[糕饼 奶奶]";
		mes "唉哟哟哟哟";
		mes "真是一个坏家伙啊";
		close;
	}
	else if ((lunar_rat >= 1 && lunar_rat <= 2) || lunar_rat == 4) {
		if (countitem(7770) >= 1 && lunar_rat == 2) {
			mes "那个是我儿子所做的糯米啊，";
			mes "拿过来给我看看吧，";
			mes "我就为了你制作出糕饼吧，";
			mes "请你稍后一下";
			delitem 7770,1; //糯米
			set lunar_rat,3;
			close;
		}
		else if (lunar_rat == 4) {
			if (countitem(7770) >= 1) {
				mes "那个是我儿子所做的糯米啊，";
				mes "拿过来给我看看吧，";
				mes "我就为了你制作出糕饼吧，";
				mes "请你稍后一下";
				delitem 7770,1; //糯米
				set lunar_rat,5;
				close;
			}
			mes "到目前为止米袋还是缺很多啊，";
			mes "若想要制作出糕饼的话";
			mes "需要更多的米袋的";
			next;
			mes "[糕饼 奶奶]";
			mes "咳嗽! 咳嗽!";
			close;
		}
		else if (countitem(7869) < 1 && lunar_rat <= 2) {
			mes "那个可以从白鼠和蓝鼠";
			mes "还有土拨鼠身上可以找到的";
			close;
		}
		mes "唉唷! 年轻人!";
		mes "你把我的米袋给找回来了，";
		mes "因为我的腰不太好的关系";
		mes "可否请你把那个米袋送到我儿子那里呢";
		set lunar_rat,2;
		close;
	}
	else if (lunar_rat == 3 || lunar_rat == 5) {
		set .@reward,rand(1,100);
		if (.@reward <= 5) {
			if (lunar_rat == 3) {
				getitem 9038,1; //新年娃娃魔物蛋
				set lunar_rat,4;
			}
			else if (lunar_rat == 5) {
				getitem 668,1; //红包
			}
		}
		else if (.@reward >= 6 && .@reward <= 10) getitem 12198,2; //幸运的糕饼
		else if (.@reward >= 11 && .@reward <= 30) getitem 12195,3; //纯味的糕饼
		else if (.@reward >= 31 && .@reward <= 40) getitem 12196,2; //饱足感的糕饼
		else if (.@reward >= 41 && .@reward <= 55) getitem 12123,2; //油果
		else if (.@reward >= 56 && .@reward <= 70) getitem 12122,2; //茶食
		else if (.@reward >= 71 && .@reward <= 80) getitem 12124,2; //彩色糕饼
		else if (.@reward >= 81 && .@reward <= 90) getitem 12198,2; //幸运的糕饼
		else if (.@reward >= 91 && .@reward <= 100) getitem 12197,3; //咸咸的糕饼
		//领取到新年娃娃魔物蛋之后，兑换时将有机率领到红包
		if (rand(1,20) <= 5 && lunar_rat == 4) getitem 668,1; //红包
		//设定任务变数，方便重新兑换
		if (lunar_rat == 3) set lunar_rat,2;
		else if (lunar_rat == 5) set lunar_rat,4;
		mes "这是为了你刚刚才做好的糕饼，";
		mes "做为答谢你是有点太简单了点";
		mes "不过是我的心意请收下吧";
		next;
		mes "[糕饼 奶奶]";
		mes "真的谢谢你啊..";
		mes "若你能带来更多的糯米的话";
		mes "我会更答谢你的";
		close;
	}
}

payon_in01,7,87,0	script	辗米厂 叔叔#rat	54,{
	mes "[辗米厂 叔叔]";
	if (!lunar_rat) {
		mes "唉哟 这些坏鼠辈们..";
		mes "把母亲辛辛苦苦所准备的米";
		mes "都给偷走了...";
		mes "唉哟.. 只要我一想到母亲的话";
		mes "就会更心痛的..";
		close;
	}
	else if (lunar_rat == 1) {
		mes "听说你要帮助我们啊";
		mes "那是真的吗?";
		mes "我对你抱着非常感激的心啊，";
		mes "当你找到米袋的同时";
		mes "就拿去给我母亲看吧";
		close;
	}
	else if (lunar_rat >= 2) {
		if (countitem(7869) < 1) {
			mes "你带多少米袋来";
			mes "我就帮你制作出多少糯米吧";
			close;
		}
		mes "你是从鼠辈那里找回米袋的吗?";
		mes "拿过来给我看看，";
		mes "现在为了母亲能制做出糕饼";
		mes "就马上制作吧";
		next;
		mes "[辗米厂 叔叔]";
		mes "由于我熟练的技巧";
		mes "在几秒钟之内就可以制作完成的，";
		mes "请你稍后一下";
		next;
		mes "[辗米厂 叔叔]";
		mes "用力!";
		next;
		mes "[辗米厂 叔叔]";
		mes "用力! 用力!";
		next;
		mes "[辗米厂 叔叔]";
		mes "用力! 用力!";
		mes "用力啦 用力!";
		next;
		mes "[辗米厂 叔叔]";
		mes "都做好了，";
		mes "为了能让母亲做糕饼";
		mes "请快一点把糯米送给她吧";
		delitem 7869,2; //米袋
		getitem 7770,1; //糯米
		close;
	}
}