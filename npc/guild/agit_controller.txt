//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 攻城战开启 控制脚本 - 
//===================================
// 版本：1.9
//===================================
// 若想知道如何设定攻城战开启时间，请到doc\woe_time_explanation.txt
// 里面的 WoE Start/Stop times
//======================================

-	script	Agit_Event	-1,{
	end;

OnClock2100:	//开始时间 星期六(6)
OnClock2300:	//结束时间 星期六(6)

OnAgitInit:
	// 开始时间检查
	if( (gettime(4)==6) && (gettime(3)>=21 && gettime(3)<23) ) {
		if (!agitcheck()) {
			AgitStart;
			callsub S_DisplayOwners;
		}
		end;
	}

	// 结束时间检查
	if ( (gettime(4)==6) && (gettime(3)==23) ) {
		if (agitcheck()) {
			AgitEnd;
			callsub S_DisplayOwners;
		}
		end;
	}
	end;

S_DisplayOwners:
	setarray .@maps$[0],"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05";
	setarray .@maps$[5],"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05";
	setarray .@maps$[10],"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05";
	setarray .@maps$[15],"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05";
	for( set .@i, 0; .@i <= 19; set .@i, .@i+1 ) {
		if (GetCastleData(.@maps$[.@i],1)) {
			Announce "秘密基地 [" + GetCastleName(.@maps$[.@i]) + "]被 [" + GetGuildName(GetCastleData(.@maps$[.@i],1)) + "] 工会占领了",bc_all|bc_woe;
		}
		else {
			Announce "秘密基地 [" + GetCastleName(.@maps$[.@i]) + "]被 [] 工会占领了",bc_all|bc_woe;
		}
	}
	end;
}