//============================================================
// Renewal Refiners
//
// v1.1 [5511]
//
// 最新编辑日期: 2011.07.21
//------------------------------------------------------------
// 更新日志
// v1.1.20110721 初版
// v1.1.20120605 加入官方精炼度判断的设置
//------------------------------------------------------------

//============================================================
// +11~+20防具/武器 精炼师
//============================================================
prt_in,90,72,5	script	贝斯特里#prt	826,{
	callfunc "F_NewRefine","贝斯特里",0;
	end;
}

morocc_in,64,41,5	script	贝斯特里#moc	826,{
	callfunc "F_NewRefine","贝斯特里",0;
	end;
}

payon_in01,18,132,3	script	贝斯特里#pay	826,{
	callfunc "F_NewRefine","贝斯特里",0;
	end;
}

//============================================================
// +11~+20防具/武器 精炼函数
//============================================================
//= 在官方脚本中似乎会生成一个1~3的随机数作为精炼结果, 其作用分别如下:
//= 1: 精炼成功
//= 2: 降低精炼度
//= 3: 精炼失败
//=
//= 若需要启用以上, 请设置第二个参数为'1'.
//= 默认情况将使用refine_db.txt文件中的成功百分率.
//============================================================
function	script	F_NewRefine	{
	disable_items;
	mes "[" + getarg(0) + "]";
	mes "我是世界上最好的铁匠" + getarg(0) + "！";
	mes "对于普通的装备，我不提供";
	mes "精炼服务，我只对精炼值在";
	mes "10 以上的装备进行精炼。";
	next;
	mes "[" + getarg(0) + "]";
	mes "如果你装备的装备中有精炼";
	mes "值在10 以上的装备的话，";
	mes "我可以帮你进行精炼。";
	mes "你想对哪个装备进行精炼呢？";
	next;

	setarray .@position$[1], "头部","身体","左手","右手","披肩","鞋子","装饰品1","装饰品2","头部2","头部3";
	set .@menu$,"";
	for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
		if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + "[" + .@position$[.@i] + " - " + getequipname(.@i) + "]";
		else
			set .@menu$, .@menu$ + "[" + .@position$[.@i] + "]";

		set .@menu$, .@menu$ + ":";
	}
	set .@part,select(.@menu$);
	
	if(!getequipisequiped(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "没有东西，我要精炼什么。";
		emotion e_an;
		close2;
		enable_items;
		end;
	}
	//检查物品是否可精炼...
	if(!getequipisenableref(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "这个东西就算是我也不可能精炼。没办法。";
		close2;
		enable_items;
		end;
	}
	//检查物品是否被鉴定... (不知道为什么会在这里... 留着它吧)
	if(!getequipisidentify(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "没鉴定过的东西拿来干嘛？";
		close2;
		enable_items;
		end;
	}
	//检查物品是否至少已经精炼+10
	if(getequiprefinerycnt(.@part) < 10) {
		mes "[" + getarg(0) + "]";
		mes "我不是说过了吗？我这里只接收精炼度在10以上的装备。";
		close2;
		enable_items;
		end;
	}
	// Custom
	//检查物品是否已经精炼+20
	if(getequiprefinerycnt(.@part) >= 20) {
		mes "[" + getarg(0) + "]";
		mes "它已经达到最大精炼度了!";
		close2;
		enable_items;
		end;
	}
	set .@refineitemid, getequipid(.@part); //存储物品ID
	set .@refinerycnt, getequiprefinerycnt(.@part); //存储已精炼数

	switch(getequipweaponlv(.@part)){
	case 1: // 武器(级数)
	case 2:
	case 3:
	case 4:
		set .@price,100000;
		set .@material,6224; // 魔力之石
		break;
	default: // 防具
		set .@price,100000;
		set .@material,6223; // 混沌金属
		break;
	}
	mes "[" + getarg(0) + "]";
	mes "我需要^003366"+getitemname(.@material)+"^000000和" + .@price + " 的精炼费.";
	mes "确认要精炼吗?";
	next;
	if(select("是的:不") == 2){
		mes "[" + getarg(0) + "]";
		mes "好...";
		mes "别着急. 一切随你.";
		close2;
		enable_items;
		end;
	}
	mes "[" + getarg(0) + "]";
	mes "不过我还是要再次提醒你精炼的危险性。";
	next;
	mes "["+getarg(0)+"]";
	mes "精炼的成功率是很低的。";
	mes "再次精炼，这件装备的^FF0000精炼度可能会降低^000000。";
	mes "如果失败，你可能会^FF0000失去这件装备^000000，";
	mes "你仍想要精炼吗?";
	next;
	if(select("是的:不") == 2){
		mes "[" + getarg(0) + "]";
		mes "其实我也认为没太有必要冒这个险。";
		close2;
		enable_items;
		end;
	}
	if((countitem(.@material) < 1) || (Zeny < .@price)) {
		mes "[" + getarg(0) + "]";
		mes "材料不够或者金钱不够的话，";
		mes "我是没办法帮你精炼的。你";
		mes "先确认一下，然后再来找我。";
		close2;
		enable_items;
		end;
	}
	set Zeny,Zeny-.@price;
	delitem .@material,1;
	if(getequipisequiped(.@part) == 0) {
		mes "[" + getarg(0) + "]";
		mes "装备怎么不见了？？！";
		close2;
		enable_items;
		end;
	}
	if(getequiprefinerycnt(.@part) != .@refinerycnt || getequipid(.@part) != .@refineitemid) {
		emotion e_an;
		mes "[" + getarg(0) + "]";
		mes "你有偷换装备吧！";
		close2;
		enable_items;
		end;
	}
	if(getarg(1) == 1){
		set .@rand,rand(1,3);
		if (.@rand == 1) {
			mes "[" + getarg(0) + "]";
			mes "呵呵！";
			mes "成功了~";
			successrefitem .@part;
			emotion e_no1;
			close2;
			enable_items;
			end;
		}
		if (.@rand == 2) {
			mes "[" + getarg(0) + "]";
			mes "可恶啊!";
			mes "我说过精炼成功的几率很低的!";
			mes "不过好在装备还在。";
			failedrefitem2 .@part,3;
			if(rand(1,5) == 1)
				emotion e_cash;
			else
				emotion e_omg;
			close2;
			enable_items;
			end;
		}
		mes "[" + getarg(0) + "]";
		mes "啊啊啊啊啊！";
		mes "竟然失败了！";
		mes "抱歉没能保住你的装备……";
		failedrefitem .@part;
		close2;
		enable_items;
		end;
	}
	set .@rand,rand(100);
	if(getequippercentrefinery(.@part) > .@rand) {
		mes "[" + getarg(0) + "]";
		mes "呵呵！";
		mes "成功了~";
		successrefitem .@part;
		emotion e_no1;
		close2;
		enable_items;
		end;
	}
	else if(getequippercentrefinery(.@part) < .@rand) {
		mes "[" + getarg(0) + "]";
		mes "可恶啊!";
		mes "我说过精炼成功的几率很低的!";
		mes "不过好在装备还在。";
		failedrefitem2 .@part,3;
		if(rand(1,5) == 1)
			emotion e_cash;
		else
			emotion e_omg;
		close2;
		enable_items;
		end;
	}
	mes "[" + getarg(0) + "]";
	mes "啊啊啊啊啊！";
	mes "竟然失败了！";
	mes "抱歉没能保住你的装备……";
	failedrefitem .@part;
	close2;
	enable_items;
	end;
}

// 魔力之石/混沌金属 兑换员
//============================================================
prt_in,85,71,5	script	奥斯特里#prt	826,{
	callfunc "F_BradiumCarniumExchange","奥斯特里";
	end;
}

morocc_in,60,38,5	script	奥斯特里#moc	826,{
	callfunc "F_BradiumCarniumExchange","奥斯特里";
	end;
}

payon_in01,14,125,5	script	奥斯特里#pay	826,{
	callfunc "F_BradiumCarniumExchange","奥斯特里";
	end;
}

//============================================================
// 魔力之石/混沌金属 兑换函数
//============================================================
function	script	F_BradiumCarniumExchange	{
	if (checkweight(1201,1) == 0) {
		mes "- 稍等下!!                 -";
		mes "- 当前你携带了太多东西了.  -";
		mes "- 先尝试减轻你的重量吧.    -";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "如果你能给我3 个神之金属";
	mes "或3 个铝，就可以交换到1";
	mes "个魔力之石或1 个混沌金属。";
	mes "但我需要收取5 万的手续费。";
	next;
	switch(select("用神之金属交换魔力之石:用铝交换混沌金属:用精炼魔力之石交换混沌金属:取消")) {
	case 1:
		if ((countitem(984) > 2) && (Zeny > 49999)) {
			delitem 984,3;  // 神之金属
			set Zeny,Zeny-50000;
			getitem 6224,1;  // 魔力之石
			mes "[" + getarg(0) + "]";
			mes "OK! 来，这是魔力之石。";
			mes "希望你能好好使用。";
			close;
		}
		mes "[" + getarg(0) + "]";
		mes "材料不够或者金钱不够的话，";
		mes "我是没办法跟你交换的。你";
		mes "先确认一下，然后再来找我。";
		close;
	case 2:
		if ((countitem(985) > 2) && (Zeny > 49999)) {
			delitem 985,3;  // 铝
			set Zeny,Zeny-50000;
			getitem 6223,1;  // 混沌金属
			mes "[" + getarg(0) + "]";
			mes "OK! 来，这是混沌金属。";
			mes "希望你能好好使用。";
			close;
		}
		mes "[" + getarg(0) + "]";
		mes "材料不够或者金钱不够的话，";
		mes "我是没办法跟你交换的。你";
		mes "先确认一下，然后再来找我。";
		close;
	case 3:
		if (countitem(6090) > 0 && Zeny > 49999) {
			delitem 6090,1;  // 精炼魔力之石
			set Zeny,Zeny-50000;
			getitem 6223,1;  // 混沌金属
			mes "[" + getarg(0) + "]";
			mes "精炼魔力之石要稍微贵一点……";
			mes "我只能用1 个混沌金属来和";
			mes "你交换。希望你能好好使用。";
			close;
		}
		mes "[" + getarg(0) + "]";
		mes "材料不够或者金钱不够的话，";
		mes "我是没办法跟你交换的。你";
		mes "先确认一下，然后再来找我。";
		close;
	case 4:
		mes "[" + getarg(0) + "]";
		mes "你很无聊吗？";
		close;
	}
}