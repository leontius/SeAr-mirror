//===== eAthena Script =======================================
//= Turbo Track NPCs
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= General Turbo Track NPCs, includes Kafra and Tool Merchant
//===== Additional Comments: =================================
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Fixed exp rewarding working backwards.[L0ne_W0lf]
//=	Fixed a bug in the while loop in the guide.
//= 1.2 Fix to prevent point converter from hanging. [L0ne_W0lf]
//= 1.3 Slight optimization to the point exchanger. [L0ne_W0lf]
//============================================================

turbo_room,130,92,3	script	卡普拉 职员#tt	115,{
	cutin "kafra_03",2;
	callfunc "F_Kafra",0,8,1,40,0;

	M_Save:
		savepoint "aldebaran",168,112;
		callfunc "F_KafEnd",0,1,"in Al De Baran";

}

turbo_room,124,86,0	shop	竞速场地 商人	90,501:-1,502:-1,503:-1,504:-1,645:-1,656:-1,1065:-1,1750:-1

//This is the name on iRO as well. [Confirmed]
turbo_room,99,120,4	script	竞速场地 指导员#Bro	124,{
	mes "[竞速场地 指导员]";
	mes "为了竞速场地可以顺利的操作";
	mes "所以我在这担任指导的工作，";
	mes "有需要什么帮忙的吗?";
	next;
	while(1) {
		switch(select("介绍竞赛方式:介绍路线:有关模式说明:不想听了")) {
		case 1:
			mes "[竞速场地 指导员]";
			mes "竞赛方式很单纯，";
			mes "跨越障碍物以最快的时间";
			mes "到达决胜终点";
			next;
			mes "[竞速场地 指导员]";
			mes "当然会随着参赛者的不同";
			mes "就会发生不同的状况，";
			mes "我不能肯定的告诉你，";
			mes "一定就是会怎样的，";
			mes "这点请你谅解";
			next;
			mes "[竞速场地 指导员]";
			mes "骑士或是骑士领主、十字军、";
			mes "圣殿十字军，所骑乘的大嘴鸟的人们，";
			mes "在竞技场外面寄放大嘴鸟后";
			mes "才进来的，";
			mes "等到竞赛结束后，";
			mes "请你带回大嘴鸟吧";
			next;
			mes "[竞速场地 指导员]";
			mes "优胜者就是到达最初";
			mes "第一个决胜点的人，不管是谁";
			mes "先到达决胜点时，该竞赛就结束了，";
			mes "全部的人都会自动回到";
			mes "等待室了";
			next;
			mes "[竞速场地 指导员]";
			mes "还有竞赛开始后，经过一段时间后，";
			mes "不关有没有优胜者都没关系，";
			mes "竞赛会自动结束的，全体都会被淘汰，";
			mes "这是为了更快速进行竞赛的规则，";
			mes "这点请留意";
			next;
			mes "[竞速场地 指导员]";
			mes "对于参赛者的职业、";
			mes "等级没有限制，";
			mes "但是，参赛时会收取竞赛场入场费用，";
			mes "这一点请你不要有误会，";
			mes "入场费是1000金币";
			next;
			mes "[竞速场地 指导员]";
			mes "有关介绍竞赛方式的说明";
			mes "到此结束了，";
			mes "还有想知道的事情吗?";
			next;
			break;
		case 2:
			mes "[竞速场地 指导员]";
			mes "我来对路线做个介绍吧，";
			mes "首先对竞赛场地整体的构造";
			mes "做个说明";
			next;
			mes "[竞速场地 指导员]";
			mes "竞速场地是有";
			mes "^4A4AFF独木桥、箱子斜坡、";
			mes "诅咒和混乱的沙漠、强光陷阱迷路、";
			mes "单行道蜗牛、蛇骰子路、";
			mes "小的洞穴、看不见的迷路等，";
			mes "由这些东西所构成的^000000";
			next;
			mes "[竞速场地 指导员]";
			mes "路线的构成，或多或少跟模式的不同而有所差别";
			mes "以最基本的起点和终点之外，用合计的7种路线所构成";
			mes "现在说明一下各路线的情况吧";
			next;
			set .@exitloop,1;
			while (.@exitloop) {
				switch(select("所谓的独木桥、箱子斜坡是?:所谓的诅咒和混乱的沙漠、强光陷阱迷路是?:所谓的单行道蜗牛、看不见的迷路是?:所谓的蛇骰子路、小的洞穴是?:不用说明了")) {
				case 1:
					mes "[竞速场地 指导员]";
					mes "独木桥就像名字那样，";
					mes "是比赛最初路线通过的独木桥";
					mes "一不小心的话就会掉下去回到最初的起点";
					mes "所以请注意，一定要小心通过";
					next;
					mes "[竞速场地 指导员]";
					mes "箱子斜坡就像名字那样，";
					mes "由箱子堆积而成的丘陵";
					mes "只要越过箱子就能到达下一关";
					mes "所以不是太困难";
					next;
					break;
				case 2:
					mes "[竞速场地 指导员]";
					mes "诅咒和混乱的沙漠，只要通过沙漠就行了……";
					mes "但是会发生什么事情都无法预测";
					mes "就像它的名字一样，充满着诅咒和混乱吧";
					next;
					mes "[竞速场地 指导员]";
					mes "强光陷阱迷路，是设置了很多圈套的复杂迷宫";
					mes "曲折的水路中设置了什么圈套连我也不知道";
					next;
					break;
				case 3:
					mes "[竞速场地 指导员]";
					mes "单行道蜗牛，就像蜗牛壳那样有旋转的窄窄的小道路的路线";
					mes "里面没有什么特别的陷阱，只要直接跑过去就好了";
					mes "不过呢，不是这么好跑的";
					next;
					mes "[竞速场地 指导员]";
					mes "看不见的迷路，是一条很宽广的路线";
					mes "只要跑到出口就行了，但是里面有很多看不见的墙壁堵着道路";
					mes "所以请参考比赛过程中的提示";
					mes "因为的确是看不见的迷路";
					next;
					break;
				case 4:
					mes "[竞速场地 指导员]";
					mes "蛇骰子路，就像大蛇一样，弯弯曲曲的道路";
					mes "为什么要用骰子来建造?";
					mes "因为路上隐藏着像骰子那样偶然才能碰上幸运吧";
					next;
					mes "[竞速场地 指导员]";
					mes "想知道是什么意思或者详情的话，请直接去亲身体验吧";
					next;
					mes "[竞速场地 指导员]";
					mes "那个小的洞窟简直就是完全再现了据说在斐扬那里的某个洞窟的样子";
					mes "请好好享受洞穴的探险吧";
					next;
					break;
				case 5:
					set .@exitloop,0;
					break;
				}
			}
			mes "[竞速场地 指导员]";
			mes "知道了";
			mes "关于路线的说明，已经没问题了吧";
			next;
			mes "[竞速场地 指导员]";
			mes "还有什么其他想知道的事吗?";
			next;
			break;
		case 3:
			mes "[竞速场地 指导员]";
			mes "现在来说明一下关于各模式的情况";
			mes "比赛分一般模式、熟练模式、单人模式3种";
			next;
			mes "[竞速场地 指导员]";
			mes "有一部分的技能会被限制使用";
			mes "而且跟一般模式不同，^4A4AFF熟练模式可以对其他选手进行攻击^000000";
			mes "进行队战或公会战也是不错的";
			next;
			mes "[竞速场地 指导员]";
			mes "然后，在各个模式中，";
			mes "只有^0000FF1、16人用的竞技场会加上看不见的迷路^000000";
			mes "4人用、8人用的场地并没有这条路线";
			mes "所以请注意";
			next;
			mes "[竞速场地 指导员]";
			mes "还有，单人模式包含了所有路线";
			mes "在这里，会记录第一名的人物的名字";
			mes "大家也可以挑战，刷新纪录看看";
			next;
			break;
		case 4:
			mes "[竞速场地 指导员]";
			mes "我说的说明够充分了吗?";
			mes "祝你有个愉快的时间，";
			mes "谢谢您";
			close;
		}
	}

OnInit:
	// This will only ever be run ONCE!
	// Unless you eradicate the variable.
	//
	// This OnInit initiallizes and sets the starting
	// data for the Turbo Track's "Record" Logs.
	// WARNING/ATTENTION: DO NOT MODIFY ANY OF THE DATA.
	// DOING SO WILL CAUSE INTERNAL PROBLEMS IN THE SCRIPT.
	if ($ttnames$[0] == "") {
		setarray $ttnames$[0],"Breezy Havana";	// Solo Mode/Time Attack
		setarray $ttnames$[1],"RS125";	// Normal 4 Player
		setarray $ttnames$[2],"Hollgrehenn";	// Normal 8 Players
		setarray $ttnames$[3],"Antonio";	// Noomal 16 Players

		setarray $ttnames$[4],"Aragham";	// Expert 4 Players
		setarray $ttnames$[5],"Kafra Jasmine";	// Expert 8 Players
		setarray $ttnames$[6],"Chris";	// Expert 16 Players

		setarray $ttnames$[7],"Breezy Havana";	// Honor Place 1
		setarray $ttnames$[8],"RS125";	// Honor Place 2

		setarray $ttnames$[9],"Breezy Havana";	// Top 1
		setarray $ttnames$[10],"Nari";	// Top 2
		setarray $ttnames$[11],"Senorita Sylvia";	// Top 3
		setarray $ttnames$[12],"Joo Jahk";	// Top 4
		setarray $ttnames$[13],"RS125";	// Top 5

		setarray $ttranks[0],999999,0,0,0,0,0;
	}
	end;

Onend_n1:
	mapannounce "turbo_room","A [Normal Mode - Solo] game will end shortly.",bc_map,"0xadff2f";
	end;

Onend_n4:
	mapannounce "turbo_room","A [Normal Mode - 4 Person] game will end shortly.",bc_map,"0xadff2f";
	end;

Onend_n8:
	mapannounce "turbo_room","A [Normal Mode - 8 Person] game will end shortly.",bc_map,"0xadff2f";
	end;

Onend_n16:
	mapannounce "turbo_room","A [Normal Mode - 16 Person] game will end shortly.",bc_map,"0xadff2f";
	end;

Onend_e4:
	mapannounce "turbo_room","A [Expert Mode - 4 Person] game will end shortly.",bc_map,"0xadff2f";
	end;

Onend_e8:
	mapannounce "turbo_room","A [Expert Mode - 8 Person] game will end shortly.",bc_map,"0xadff2f";
	end;

Onend_e16:
	mapannounce "turbo_room","A [Expert Mode - 16 Person] game will end shortly.",bc_map,"0xadff2f";
	end;

Onnew_top1:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[9] + " has ranked Number One in the Turbo Track Hall of Honor!",bc_map,"0x00ff00";
	end;

Onnew_top2:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[10] + " has ranked Second in the Turbo Track Hall of Honor!",bc_map,"0x00ff00";
	end;

Onnew_top3:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[11] + " has ranked Third in the Turbo Track Hall of Honor!",bc_map,"0x00ff00";
	end;

Onnew_top4:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[12] + " has ranked Fourth in the Turbo Track Hall of Honor!",bc_map,"0x00ff00";
	end;

Onnew_top5:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[13] + " has ranked Fifth in the Turbo Track Hall of Honor!",bc_map,"0x00ff00";
	end;

Onwin_n4:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[1] + " just won a [Normal Mode - 4 Person] game!",bc_map,"0x70DBDB";
	end;

Onwin_n8:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[2] + " just won a [Normal Mode - 8 Person] game!",bc_map,"0x70DBDB";
	end;

Onwin_n16:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[3] + " just won a [Normal Mode - 16 Person] game!",bc_map,"0x70DBDB";
	end;

Onwin_e4:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[4] + " just won an [Expert Mode - 4 Person] game!",bc_map,"0x70DBDB";
	end;

Onwin_e8:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[5] + " just won an [Expert Mode - 8 Person] game!",bc_map,"0x70DBDB";
	end;

Onwin_e16:
	mapannounce "turbo_room","Congratulations! " + $ttnames$[6] + " just won an [Expert Mode - 16 Person] game!",bc_map,"0x70DBDB";
	end;
}

turbo_room,102,117,3	script	Hall of Honor#tt	857,{
	mes "^2F4F4FTurbo Track Hall of Honor^000000";
	mes " ";
	mes "The First:";
	mes "^4d4dff" + $ttnames$[7] + "^000000";
	mes " ";
	mes "The Last:";
	mes "^4d4dff" + $ttnames$[8] + "^000000";
	close;
}

turbo_room,77,115,5	script	Solo Mode#tt::TurboSoloNames	857,{
	mes "^2F4F4FSolo Mode Record^000000";
	mes " ";
	mes "The best player";
	mes "in Solo Mode...";
	mes "^4d4dff" + $ttnames$[0] + "^000000 !";
	close;
}

turbo_room,87,114,5	script	Normal Mode Record#tt::TurboNormalNames	857,{
	mes "<<Recent Top Players>>";
	mes "Winners of Normal Mode - 4 Person";
	mes "^4d4dff" + $ttnames$[1] + "^000000";
	mes "Winners of Normal Mode - 8 Person";
	mes "^4d4dff" + $ttnames$[2] + "^000000";
	mes "Winners of Normal Mode - 16 Person";
	mes "^4d4dff" + $ttnames$[3] + "^000000";
	close;
}

turbo_room,112,114,3	script	Expert Mode Record::TurboExpertNames	857,{
	mes "<<Recent Top Players>>";
	mes "Winners of Expert Mode - 4 Person";
	mes "^4d4dff" + $ttnames$[4] + "^000000";
	mes "Winners of Expert Mode - 8 Person";
	mes "^4d4dff" + $ttnames$[5] + "^000000";
	mes "Winners of Expert Mode - 16 Person";
	mes "^4d4dff" + $ttnames$[6] + "^000000";
	close;
}

turbo_room,97,117,5	script	Hall of Honor#tt2	857,{
	mes "^2F4F4FTurbo Track Hall of Honor^000000";
	mes " ";
	mes "1st: ^4d4dff" + $ttnames$[9] + "^000000";
	mes "2nd: ^4d4dff" + $ttnames$[10] + "^000000";
	mes "3rd: ^4d4dff" + $ttnames$[11] + "^000000";
	mes "4th: ^4d4dff" + $ttnames$[12] + "^000000";
	mes "5th: ^4d4dff" + $ttnames$[13] + "^000000";
	close;
}

turbo_room,75,95,5	duplicate(TurboSoloNames)	Solo Mode#1	857
turbo_room,67,95,5	duplicate(TurboNormalNames)	Normal Mode Records#1	857
turbo_room,71,95,5	duplicate(TurboExpertNames)	Expert Mode Records#1	857

// Turbo Track Item Exchanger
turbo_room,93,117,5	script	点数商品 介绍	125,{
	//set max_max_c1,1201;
	//if (max_max_c1 == 1) {
	//	mes "^3355FFWait a second!";
	//	mes "Right now, you're carrying";
	//	mes "too many items with you.";
	//	mes "Please come back after";
	//	mes "putting storing some of your";
	//	mes "things using the Kafra Service.^000000";
	//	close;
	//}
	set .@now_weight,MaxWeight-Weight;
	if (.@now_weight < 2000) {
		mes "^3355FFWait a second!";
		mes "Right now, you're carrying";
		mes "too many items with you.";
		mes "Please come back after";
		mes "putting storing some of your";
		mes "things using the Kafra Service.^000000";
		close;
	}
	if (tt_point < 10) {
		mes "[商品 交换员]";
		mes "Good day,";
		mes "" + strcharinfo(0) + ".";
		mes "You've got a total of";
		mes "" + tt_point + " Turbo Track Points.";
		next;
		mes "[商品 交换员]";
		mes "If you have at least";
		mes "10 Turbo Track Points,";
		mes "you can exchange these";
		mes "points for items. Would";
		mes "you like to see the Turbo";
		mes "Track Point exchange list?";
		next;
		if (select("Yes.:Cancel.") == 1) {
			mes "[商品 交换员]";
			mes "^3355FF10 TTP^000000: 1 Free Ticket";
			mes "for Kafra Transportation";
			mes "^3355FF12 TTP^000000: 1 Level 5 Magic Scroll";
			mes "^3355FF40 TTP^000000: Experience Points";
			next;
			mes "[商品 交换员]";
			mes "^3355FF150 TTP^000000: 1 3 Carat Diamond";
			mes "^3355FF300 TTP^000000: 1 Gift Box";
			mes "^3355FF400 TTP^000000: 1 Speed Potion";
			next;
			mes "[商品 交换员]";
			mes "You can also convert Turbo Track Points into Arena Points. For more information, please speak to the Arena Point Manager. Thank you.";
			close;
		}
		mes "[商品 交换员]";
		mes "We hope that you enjoy";
		mes "Al De Baran's Turbo Track.";
		mes "Try to earn as many points";
		mes "as you can so that you can";
		mes "exchange them for useful";
		mes "stuff later. Happy racing~";
		close;
	}
	else {
		mes "[商品 交换员]";
		mes "Good day,";
		mes "" + strcharinfo(0) + ".";
		mes "You've got a total of";
		mes "" + tt_point + " Turbo Track Points.";
		mes "Would you like to exchange";
		mes "these points for items?";
		next;
		switch(select("Exchange.:Cancel.")) {
		case 1:
			mes "[商品 交换员]";
			mes "Please choose the item for";
			mes "which you'd like to exchange";
			mes "your Turbo Track Points.";
			next;
			switch(select("Free Ticket for Kafra Transportation:Level 5 Magic Scroll:Experience Points:3 Carat Diamond:Gift Box:Speed Potion:Cancel.")) {
			case 1:
				mes "[商品 交换员]";
				mes "Each Free Ticket for";
				mes "Kafra Transportation";
				mes "costs 10 Turbo Track Points.";
				mes "Please enter the amount that";
				mes "you wish to have. To cancel,";
				mes "please enter ''^3355FF0^000000.''";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[商品 交换员]";
					mes "You have";
					mes "canceled";
					mes "your request.";
					close;
				}
				else if (.@input < 0 || .@input > 50) {
					mes "[商品 交换员]";
					mes "Your request exceeds";
					mes "the maximum limit. You";
					mes "can only receive a maximum";
					mes "of 50 tickets at once.";
					close;
				}
				else {
					set .@total_point,10 * .@input;
					if (.@total_point > tt_point) {
						mes "[商品 交换员]";
						mes "I'm sorry, but you do";
						mes "not have enough Turbo";
						mes "Track Points. Please check";
						mes "the amount of Turbo Track";
						mes "Points you have earned before";
						mes "redeeming your points again.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					getitem 7060,.@input; //Warp_Free_Ticket
					mes "[商品 交换员]";
					mes "Thank you for";
					mes "your patronage.";
					mes "We hope you enjoy";
					mes "your time here in";
					mes "the Turbo Track~";
					close;
				}
			case 2:
				mes "[商品 交换员]";
				mes "Each Magic Scroll costs";
				mes "12 Turbo Track Points. Please";
				mes "select the Magic Scroll you would like to receive. Each scroll enables a one time use of a Level 5 spell or skill.";
				next;
				switch(select("Earth Spike:Cold Bolt:Fire Bolt:Lightning Bolt:Soul Strike:Fire Ball:Fire Wall:Frost Diver:Heal")) {
				case 1:
					set .@scroll,687; //Earth_Scroll_1_5
					set .@name$,"Earth Spike";
					break;
				case 2:
					set .@scroll,689; //Cold_Scroll_1_5
					set .@name$,"Cold Boltl";
					break;
				case 3:
					set .@scroll,691; //Fire_Scroll_1_5
					set .@name$,"Fire Bolt";
					break;
				case 4:
					set .@scroll,693; //Wind_Scroll_1_5
					set .@name$,"Lightning Bolt";
					break;
				case 5:
					set .@scroll,695; //Ghost_Scroll_1_5
					set .@name$,"Soul Strike";
					break;
				case 6:
					set .@scroll,697; //Fire_Scroll_2_5
					set .@name$,"Fire Ball";
					break;
				case 7:
					set .@scroll,699; //Fire_Scroll_3_5
					set .@name$,"Fire Wall";
					break;
				case 8:
					set .@scroll,12000; //Cold_Scroll_2_5
					set .@name$,"Frost Diver";
					break;
				case 9:
					set .@scroll,12002; //Holy_Scroll_1_5
					set .@name$,"Heal";
					break;
				}
				mes "[商品 交换员]";
				mes "Please enter the number";
				mes "of ^4d4dff"+.@name$+"^000000 Magic Scrolls that";
				mes "you would like to receive.";
				mes "To cancel, enter ''^3355FF0^000000.''";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[商品 交换员]";
					mes "You have";
					mes "canceled";
					mes "your request.";
					close;
				}
				else if (.@input < 0 || .@input > 50) {
					mes "[商品 交换员]";
					mes "I'm sorry, but your";
					mes "request has exceeded the";
					mes "maximum limit. You can only";
					mes "request up to 50 scrolls at once. ^FFFFFFcobo^000000";
					close;
				}
				else {
					set .@total_point,12 * .@input;
					if (.@total_point > tt_point) {
						mes "[商品 交换员]";
						mes "I'm sorry, but you do";
						mes "not have enough Turbo";
						mes "Track Points. Please check";
						mes "the amount of Turbo Track";
						mes "Points you have earned before";
						mes "redeeming your points again.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					getitem .@scroll,.@input;
					mes "[商品 交换员]";
					mes "Thank you for";
					mes "your patronage.";
					mes "We hope you enjoy";
					mes "your time here in";
					mes "the Turbo Track~";
					close;
				}
			case 3:
				mes "[商品 交换员]";
				mes "You can exchange";
				mes "40 Turbo Track Points";
				mes "to receive Base Level";
				mes "Experience. Would you";
				mes "like to exchange your Turbo";
				mes "Track Points for Experience?";
				next;
				if (select("No:Yes") == 2) {
					if (tt_point < 40) {
						mes "[商品 交换员]";
						mes "I'm sorry, but you do";
						mes "not have enough Turbo";
						mes "Track Points. Please check";
						mes "the amount of Turbo Track";
						mes "Points you have earned before";
						mes "redeeming your points again.";
						close;
	
					}
					set tt_point,tt_point-40;
					if (BaseLevel < 70)
						getexp 3000,0;
					else if (BaseLevel < 80)
						getexp 9000,0;
					else if (BaseLevel < 90)
						getexp 10000,0;
					else if (BaseLevel < 100)
						getexp 30000,0;
					mes "[商品 交换员]";
					mes "Thank you, your";
					mes "Turbo Track Points";
					mes "have been converted into";
					mes "Base Level Experience.";
					close;
				}
				mes "[商品 交换员]";
				mes "You have";
				mes "canceled";
				mes "your request.";
				close;
			case 4:
				mes "[商品 交换员]";
				mes "You can exchange";
				mes "150 Turbo Track Points";
				mes "for ^3131FF1 3 Carat Diamond^000000.";
				mes "Please enter the number of";
				mes "diamonds you would like to receive.";
				mes "To cancel, enter ''^3355FF0^000000.''";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[商品 交换员]";
					mes "You have";
					mes "canceled";
					mes "your request.";
					close;
				}
				else if (.@input < 0 || .@input > 10) {
					mes "[商品 交换员]";
					mes "Your request exceeds";
					mes "the maximum limit. You";
					mes "can only receive a maximum";
					mes "of 10 diamonds at once.";
					close;
				}
				else {
					set .@total_point,150 * .@input;
					if (.@total_point > tt_point) {
						mes "[商品 交换员]";
						mes "I'm sorry, but you do";
						mes "not have enough Turbo";
						mes "Track Points. Please check";
						mes "the amount of Turbo Track";
						mes "Points you have earned before";
						mes "redeeming your points again.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					getitem 732,.@input; //Crystal_Jewel__
					mes "[商品 交换员]";
					mes "Thank you for";
					mes "your patronage.";
					mes "We hope you enjoy";
					mes "your time here in";
					mes "the Turbo Track~";
					close;
				}
			case 5:
				mes "[商品 交换员]";
				mes "You can exchange";
				mes "300 Turbo Track Points";
				mes "for ^3131FF1 Gift Box^000000. Please enter";
				mes "the number of Gift Boxes";
				mes "that you'd like to receive.";
				mes "To cancel, enter ''^3355FF0^000000.''";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[商品 交换员]";
					mes "You have";
					mes "canceled";
					mes "your request.";
					close;
				}
				else if (.@input < 0 || .@input > 10) {
					mes "[商品 交换员]";
					mes "Your request exceeds";
					mes "the maximum limit. You";
					mes "can only receive a maximum";
					mes "of 10 Gift Boxes at once.";
					close;
				}
				else {
					set .@total_point,300 * .@input;
					if (.@total_point > tt_point) {
						mes "[商品 交换员]";
						mes "I'm sorry, but you do";
						mes "not have enough Turbo";
						mes "Track Points. Please check";
						mes "the amount of Turbo Track";
						mes "Points you have earned before";
						mes "redeeming your points again.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					getitem 644,.@input; //Gift_Box
					mes "[商品 交换员]";
					mes "Thank you for";
					mes "your patronage.";
					mes "We hope you enjoy";
					mes "your time here in";
					mes "the Turbo Track~";
					close;
				}
			case 6:
				mes "[商品 交换员]";
				mes "You can exchange";
				mes "400 Turbo Track Points";
				mes "for ^3131FF1 Speed Potion^000000. Please";
				mes "enter the number of potions";
				mes "that you'd like to receive.";
				mes "To cancel, enter ''^3355FF0^000000.''";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[商品 交换员]";
					mes "You have";
					mes "canceled";
					mes "your request.";
					close;
				}
				else if (.@input < 0 || .@input > 10) {
					mes "[商品 交换员]";
					mes "Your request exceeds";
					mes "the maximum limit. You";
					mes "can only receive a maximum";
					mes "of 10 potions at once.";
					close;
				}
				else {
					set .@total_point,400 * .@input;
					if (.@total_point > tt_point) {
						mes "[商品 交换员]";
						mes "I'm sorry, but you do";
						mes "not have enough Turbo";
						mes "Track Points. Please check";
						mes "the amount of Turbo Track";
						mes "Points you have earned before";
						mes "redeeming your points again.";
						close;
					}
					set tt_point,tt_point-.@total_point;
					getitem 12016,.@input; //Speed_Up_Potion
					mes "[商品 交换员]";
					mes "Thank you for";
					mes "your patronage.";
					mes "We hope you enjoy";
					mes "your time here in";
					mes "the Turbo Track~";
					close;
				}
			case 7:
				mes "[商品 交换员]";
				mes "This Item Exchange";
				mes "Service is brought";
				mes "to you by...";
				next;
				mes "[商品 交换员]";
				mes "This Item Exchange Service";
				mes "is brought to you by the Kafra";
				mes "Corporation, Blacksmith Guild";
				mes "and the Comodo Casino and";
				mes "the Al De Baran Guild Castle";
				mes "Management Luina.";
				close;
			}
		case 2:
			mes "[商品 交换员]";
			mes "Turbo Track Points can be";
			mes "converted into Arena Points.";
			mes "You can save a maximum of";
			mes "^4D4DFF29,000 Turbo Track Points.";
			next;
			mes "[商品 交换员]";
			mes "Before reaching the maximum";
			mes "amount of Turbo Track Points,";
			mes "you might want to spend some";
			mes "of them so that you can keep";
			mes "getting your point rewards after winning Turbo Track races.";
			close;
		}
	}
}

turbo_room,106,117,3	script	竞速场地 点数管理#tt	833,{
	mes "[竞速场地 点数 服务人员]";
	mes "您好，";
	mes "今天的竞赛还愉快吗?";
	mes "来，有什么我可以帮你的嘛?";
	next;
	switch(select("确认 点数:转换 点数")) {
	case 1:
		mes "[竞速场地 点数 服务人员]";
		mes strcharinfo(0) +"的点数如下，";
		mes "竞速场地点数："+ tt_point +"点，";
		mes "竞技场点数："+ arena_point +"点";
		next;
		mes "[竞速场地 点数 服务人员]";
		mes "如果您愿意的话，可将竞技场点数转换成";
		mes "竞速场地点数，";
		mes "希望您在使用上做参考";
		close;
	case 2:
		mes "[竞速场地 点数 服务人员]";
		mes strcharinfo(0) +"的点数如下，";
		mes "竞速场地点数："+ tt_point +"点，";
		mes "竞技场点数："+ arena_point +"点";
		next;
		mes "[竞速场地 点数 服务人员]";
		mes "竞技场地点数可换成竞速场地点数，";
		mes "比率是2：1，给我10点竞技场点数，可获得5点竞速场地点数";
		next;
		mes "[竞速场地 点数 服务人员]";
		mes "请选择８点以下，";
		mes "假如你想要换算10点以上的点数时，请你";
		mes "直接输入要^4A4AFF换算成10单位几倍^000000即可";
		next;
		switch(select("竞技场2点 -> 竞速场地1点:竞技场4点 -> 竞速场地2点:竞技场6点 -> 竞速场地3点:竞技场8点 -> 竞速场地4点:竞技场点数10点单位:取消")) {
		case 1:
			callsub S_ExchangePoints,28999,2,1;
		case 2:
			callsub S_ExchangePoints,28998,4,2;
		case 3:
			callsub S_ExchangePoints,28997,6,2;
		case 4:
			callsub S_ExchangePoints,28996,8,4;
		case 5:
			mes "[竞速场地 点数 服务人员]";
			mes "竞技场点数跟竞速场地点数交换，^4A4AFF必须以10点为单位^000000";
			mes "^4A4AFF能同时交换的竞技场点数是，10点的20倍，也就是200点^000000";
			next;
			mes "[竞速场地地 点数 服务人员]";
			mes "那么，请输入要交换的倍数吧";
			mes "要取消的话请输入'0'";
			next;
			input .@input;
			if (.@input == 0) {
				mes "[竞速场地 点数 服务人员]";
				mes "You have";
				mes "canceled";
				mes "your request.";
				close;
			}
			else if (.@input > 20) {
				mes "[竞速场地 点数 服务人员]";
				mes "Your request exceeds";
				mes "the maximum limit. Please";
				mes "enter a value no greater than 20.";
				close;
			}
			else {
				set .@want_point1,10 * .@input;
				set .@want_point,5 * .@input;
				set .@my_turbo_all,tt_point+.@want_point;
				set .@my_arena_all,arena_point-.@want_point1;
				if (.@my_turbo_all > 28999) {
					mes "[竞速场地 点数 服务人员]";
					mes "Unfortunately, your Turbo Track Points will exceed the maximum limit if we proceed with point conversion. Please spend more";
					mes "of your Turbo Track Points before using this service. Thank you.";
					close;
				}
				if (.@my_arena_all < 0) {
					mes "[竞速场地 点数 服务人员]";
					mes "I am sorry, but you do";
					mes "not have enough Arena Points";
					mes "to perform this Turbo Track";
					mes "Point conversion.";
					close;
				}
				else {
					mes "[竞速场地 点数 服务人员]";
					mes "You have converted";
					mes "10 Arena Points into";
					mes "Turbo Track Points " + .@input + " times.";
					mes "A total of " + .@want_point1 + " Arena Points";
					mes "has been converted into";
					mes "" + .@want_point +" Turbo Track Points.";
					set arena_point,.@my_arena_all; //arena_point
					set tt_point,.@my_turbo_all;
					next;
					mes "[竞速场地 点数 服务人员]";
					mes "" + strcharinfo(0) + ",";
					mes "you now have";
					mes "^00688B" + tt_point + "^000000 Turbo Track Points";
					mes "and ^4682B4" + arena_point + "^000000 Arena Points.";
					mes "Thank you for your patronage.";
					close;
				}
			}
		case 6:
			mes "[竞速场地 点数 服务人员]";
			mes "You have";
			mes "canceled";
			mes "your request.";
			close;
		}
	case 3:
		// There is no dialog for this option.
		// It's possible it's missing, or maybe
		// the dialog was never translated. XD
		close;
	}

S_ExchangePoints:
	if (tt_point > getarg(0)) {
		mes "[竞速场地 点数 服务人员]";
		mes "Unfortunately, your Turbo Track Points will exceed the maximum limit if we proceed with point conversion. Please spend more";
		mes "of your Turbo Track Points before using this service. Thank you.";
		close;
	}
	if (arena_point >= getarg(1)) {
		mes "[竞速场地 点数 服务人员]";
		mes "As requested,";
		mes "2 Arena Points";
		mes "have been converted";
		mes "into 1 Turbo Track Point.";
		set arena_point,arena_point-getarg(1);
		set tt_point,tt_point+getarg(2);
		next;
		mes "[竞速场地 点数 服务人员]";
		mes "" + strcharinfo(0) + ",";
		mes "you now have";
		mes "^00688B" + tt_point + "^000000 Turbo Track Points";
		mes "and ^4682B4" + arena_point + "^000000 Arena Points.";
		mes "Thank you for your patronage.";
		close;
	}
	else {
		mes "[竞速场地 点数 服务人员]";
		mes "I'm sorry, but you do not have enough Arena Points. You need";
		mes "at least 2 Arena Points in order";
		mes "to use this service.";
		close;
	}
}

alde_gld,183,204,0	script	en_turbo	45,1,1,{
	if(checkriding()) {
		mes "[竞速场地 入场人员]";
		mes "对不起，你的大嘴鸟请让我们先领走";
		mes "我们会给你'大嘴鸟免费使用券'，请稍候再使用";
		close2;
		setoption 0x0000;
		getitem 7310,1;
	}
	warp "turbo_room",100,65;
	end;
}

alde_gld,186,199,3	script	竞速场地 入场人员#Entran	845,{
	mes "[入场人员]";
	mes "您好~ 欢迎您来到艾尔帕兰竞速场地竞赛场";
	next;
	mes "[入场人员]";
	mes "艾尔帕兰";
	mes "竞速场地竞赛场，是禁止骑乘大嘴鸟入场的，";
	mes "如果你在入口骑乘大嘴鸟时，会自动解除";
	mes "另外我们会给你'大嘴鸟免费使用券'请你不要担心";
	next;
	mes "[入场人员]";
	mes "那么请尽情享受竞速场地的竞赛吧~";
	close;
}

alde_gld,181,199,5	script	大嘴鸟 管理人员	845,{
	mes "[大嘴鸟 管理人员]";
	if (getskilllv(63) == 0) {
		mes "感谢您来到艾尔帕兰 竞速场地竞赛场~";
		close;
	}
	mes "欢迎光临~";
	mes "在这里可以领取大嘴鸟~";
	mes "只要2,500Zeny或者持有'大嘴鸟免费使用券'就可以了";
	mes "怎么样~?";
	next;
	switch(select("好的，我要乘坐大嘴鸟:还是不要了")) {
	case 1:
		if (checkriding()) {
			mes "[大嘴鸟 管理人员]";
			mes "你已经在乘坐大嘴鸟了~";
			mes "请你再看看自己现在的样子~";
			close;
		}
		else {
			if (countitem(7310) < 1 && Zeny < 2500) {
				mes "[大嘴鸟 管理人员]";
				mes "喔~你好像没有使用券或足够的金钱~";
				mes "要使用服务的话，请按照标准价格哦~";
				close;
			}
			else {
				if (countitem(7310) > 0)
					delitem 7310,1;
				else
					set zeny,zeny-2500;
				setriding;
				mes "[大嘴鸟 管理人员]";
				mes "谢谢使用~";
				close;
			}
		}
	case 2:
		mes "[大嘴鸟 管理人员]";
		mes "如果有空的话也请参加比赛吧~";
		mes "希望你能获胜~";
		close;
	}
}

alde_gld,178,180,0	script	标示牌#TBT	837,{
	mes "[艾尔帕兰 竞速场地]";
	mes "从这条路进去的话，就可以进入到艾尔帕兰 竞速场地";
	next;
	mes "[艾尔帕兰 竞速场地]";
	mes "竞速场地是要竞争到目的地为止，并通过各种路线的竞赛场，";
	mes "各种设备正在等着各位，请每样都去试试看吧";
	close;
}
