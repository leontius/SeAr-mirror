//========================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// - 深渊湖泊 传送脚本 -
//========================
//= 版本：1.4
//========================

hu_fild05,168,304,0	script	梁柱#abyss1	111,{
	if ((countitem(1035) < 1) || (countitem(1036) < 1) || (countitem(1037) < 1)) { 
		mes "-在梁柱的表面上-";
		mes "-好像有类似脱落的一些小凹槽-";
		mes "-整体的形状和龙的形象类似-";
		mes "-好像是从这三个小凹槽中-";
		mes "-物品各自脱落出来似的-";
		close;
	}
	mes "-在梁柱的表面上-";
	mes "-好像有类似脱落的小凹槽-";
	mes "-那和龙的形象类似-";
	next;
	if (select("在梁柱的凹槽上放入物品:在你的嘴里放入物品")==1){
		mes "-把龙牙小心翼翼地-";
		mes "-嵌入凹槽中-";
		mes "-梁柱上发出嘶吼的声音-";
		specialeffect 72;
		next;
		mes "-这次在别的凹槽上-";
		mes "-小心翼翼地把龙鳞嵌入-";
		mes "-梁柱上发出嘶吼声及耸动一下-";
		specialeffect 72;
		next;
		mes "-最后把龙尾小心翼翼地-";
		mes "-嵌入到剩余的凹槽内-";
		mes "-结果发出嘶吼声的同时-";
		mes "-从梁柱裂缝中开始渗出光线来-";
		mes "-光线缠绕着你的全身-";
		specialeffect 72;
		next;
		mes "-似乎往某处被拉走般-";
		mes "-感觉脚底消失不见一样-";
		specialeffect 99;
		donpcevent "AbyssWarp::OnWarp";
		specialeffect2 61;
		delitem 1035,1;
		delitem 1036,1;
		delitem 1037,1;
		close2;
		warp "hu_fild05",184,204;
		end;
	}
	mes "-你把那些小心翼翼地-";
	mes "-放入到你的嘴巴里-";
	mes "-结果发出嘶吼声的同时-";
	mes "-肚子开始骚动起来-";
	next;
	emotion 23,1;
	delitem 1035,1;
	delitem 1036,1;
	delitem 1037,1;
	percentheal -10,0;
	close;
}

hu_fild05,171,211,0	script	梁柱#abyss2	111,{

	mes "-和湖边看过的梁柱类似-";
	mes "-原本凹槽的位置上发出蓝光-";
	next;
	if(select("在发亮的位置上用手去触碰:在发亮的位置上把物品挖出来")==1){
		mes "-(咚咚咚)-";
		next;
		mes "-光线被扭曲后-";
		mes "-从里面传出声音来-";
		specialeffect 72;
		next;
		mes "-(咯咯咯咯咯咯咯)-";
		mes "-(砰)-";
		next;
		mes "-在凹槽的地方一阵摇晃后-";
		mes "-从梁柱内射出光线来-";
		mes "-似乎往某处被拉走般-";
		mes "-感觉脚底消失不见一样-";
		next;
		specialeffect 99;
		specialeffect2 61;
		close2;
		warp "hu_fild05",169,305;
		end;
	}
	mes "-(喀嚓!)-";
	mes "-挖出来的物品碎掉了-";
	specialeffect 72;
	next;
	mes "-(砰砰砰砰砰砰砰砰)-";
	next;
	mes "-光线被扭曲后梁柱摇晃着-";
	mes "-好像往某处被吸走般-";
	mes "-脚底经过一阵摇晃后-";
	mes "-你就朝某处逃之夭夭了-";
	next;
	specialeffect 99;
	specialeffect2 61;
	close2;
	warp "hu_fild05",157,284;
	end;
}

hu_fild05,196,210,1	script	AbyssWarp	45,2,2,{

OnInit:
	disablenpc "AbyssWarp";
	end;

OnTouch:
	warp "abyss_01",260,268;
	end;

OnWarp:
	initnpctimer;
	enablenpc "AbyssWarp";
	end;

OnTimer30000:
	stopnpctimer;
	disablenpc "AbyssWarp";
	end;
}
